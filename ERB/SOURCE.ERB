;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@SOURCE_CHECK
	SELECTCOM = TFLAG:3
	;調教SELECTCOMの時間経過
	CALL TRACHECK_TIME
	;-------------------------------------------------
	;身体接触の継続、解除
	;-------------------------------------------------
	CALL TOUCH_SUCCESSION(TARGET)
	;カウンター
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		CALL EVENT_COUNTER(TARGET:LOCAL,LOCAL)
		CALL EVENT_COUNTER_SOURCE(TARGET:LOCAL,LOCAL)
	NEXT
	CALL EVENT_COUNTER_COMBINATION
	FOR LOCAL,0,CHARANUM
		;性交フラグの設定
		IF TEQUIP:LOCAL:膣 == 1
			TEQUIP:LOCAL:Ｖセックス = MASTER
		ELSE
			TEQUIP:LOCAL:Ｖセックス = -1
		ENDIF
		IF TEQUIP:LOCAL:アナル == 1
			TEQUIP:LOCAL:Ａセックス = MASTER
		ELSE
			TEQUIP:LOCAL:Ａセックス = -1
		ENDIF
		;逆レイプ
		SIF TEQUIP:LOCAL:ペニス == 6
			TEQUIP:PLAYER:Ｖセックス = LOCAL
		;逆アナル
		SIF TEQUIP:LOCAL:ペニス == 7
			TEQUIP:PLAYER:Ａセックス = LOCAL
		;体位リセット
		SIF TEQUIP:LOCAL:Ｖセックス < 0 && TEQUIP:LOCAL:Ａセックス < 0
			TEQUIP:LOCAL:体位 = 0
		;ついでに勃起度の保存
		CFLAG:LOCAL:勃起度 = BASE:LOCAL:勃起
	NEXT
	;-------------------------------------------------
	;@COMの呼び出し
	;-------------------------------------------------
	CALL CALL_COM(TARGET)
	IF RESULT:0 == 0
		RETURN 0
	ENDIF
	LOCAL:100 = RESULT
	;コマンド実行失敗時スキップ
	IF !LOCAL:100
		FOR LOCAL,0,CHARANUM
			;体力・気力の減少
			CALL SOURCE_DOWNBASE(LOCAL)
		NEXT
		GOTO SKIP
	ENDIF
	FOR LOCAL,0,CHARANUM
		;ローター挿入
		IF CFLAG:LOCAL:ローター挿入
			IF RAND:3 == 0 && LOCAL != 0
				IF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置
					PRINTFORMW 低くうねるような音に%CALLNAME:MASTER%が視線をむけると%CALLNAME:LOCAL%は頬を染めた。
					IF TALENT:TARGET:公衆便所
						PRINTFORMW %CALLNAME:LOCAL%は喘ぎ声を上げながらスカートをたくし上げて、ローターが挿入されている股間をさらした。
					ELSEIF TALENT:TARGET:NTR
						PRINTFORMW %CALLNAME:LOCAL%はうっとりした顔で腰を揺すっている。
					ELSE 
						PRINTFORM %CALLNAME:LOCAL%は視線を逸らし、なにやら内股ぎみにもじもじしている。
					ENDIF
				ENDIF
			ENDIF
			SOURCE:LOCAL:快Ｖ += TIME_PROGRESS(TFLAG:300) * 10
			CFLAG:LOCAL:ローター挿入 = MAX(CFLAG:LOCAL:ローター挿入 - TIME_PROGRESS(TFLAG:300),0)
			;訪問者のローターであれば屈服度加算
			IF CFLAG:LOCAL:ローター挿入者 == 21
				CFLAG:LOCAL:屈服度 ++
			ENDIF
			IF CFLAG:LOCAL:ローター挿入 == 0
				PRINTFORMW %CALLNAME:LOCAL%が装着していたローターの電池が切れた
				CFLAG:LOCAL:ローター挿入者 = 0
				IF TALENT:LOCAL:処女 == 0
					EXP:LOCAL:Ｖ経験 += 9
				ELSE
					TFLAG:2 = 1
				ENDIF
			ENDIF
		ENDIF
		IF CFLAG:LOCAL:ローターA挿入
			IF RAND:3 == 0 && LOCAL != 0 && !CFLAG:LOCAL:ローター挿入
				IF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置
					PRINTFORMW 低くうねるような音に%CALLNAME:MASTER%が視線をむけると%CALLNAME:LOCAL%は頬を染めた。
					IF TALENT:TARGET:公衆便所
						PRINTFORMW %CALLNAME:LOCAL%は喘ぎ声を上げながら後ろへ振り返りスカートをめくり上げて、ローターが挿入されているアナルをさらした。
					ELSEIF TALENT:TARGET:NTR
						PRINTFORMW %CALLNAME:LOCAL%はうっとりした顔でお尻を振っている。
					ELSE
						PRINTFORMW %CALLNAME:LOCAL%はむずむずするのか、お尻を気にしているようだ。
					ENDIF
				ENDIF
			ENDIF
			SOURCE:LOCAL:快Ａ += TIME_PROGRESS(TFLAG:300) * 10
			CFLAG:LOCAL:ローターA挿入 = MAX(CFLAG:LOCAL:ローターA挿入 - TIME_PROGRESS(TFLAG:300),0)
			;訪問者のローターであれば屈服度加算
			IF CFLAG:LOCAL:ローター挿入者 == 21
				CFLAG:LOCAL:屈服度 ++
			ENDIF
			IF CFLAG:LOCAL:ローターA挿入 == 0
				PRINTFORMW %CALLNAME:LOCAL%が装着していたローターの電池が切れた
				CFLAG:LOCAL:ローター挿入者 = 0
				EXP:LOCAL:Ａ経験 += 9
			ENDIF
		ENDIF
		;精液残留中
		IF CFLAG:LOCAL:精液残留中
			CFLAG:LOCAL:精液残留中 = MAX(CFLAG:LOCAL:精液残留中 - TIME_PROGRESS(TFLAG:300),0)
			IF CFLAG:LOCAL:精液残留中 == 0
				CFLAG:LOCAL:精液残留中 = 0
				;ターゲットとあなたと訪問者が一緒
				IF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置 && CFLAG:MASTER:現在位置 == FLAG:訪問者の現在位置
					PRINTFORML %CALLNAME:LOCAL%は慌てたかのような表情をして、急に%SHOW_BOTTOM(LOCAL,1)%の下腹部を手で押さえた。
					PRINTFORML 何故かは分からないが頬を染めて明らかに動揺しているようだ。
					IF RAND:2 == 0 && CFLAG:LOCAL:服装_下半身下着２ == 0
						PRINTFORML %CALLNAME:MASTER%が心配していると、そのまま%CALLNAME:LOCAL%が耳まで真っ赤にして俯いてしまった。
						PRINTFORMW そしてその様子を離れて見ていた%NTR_NAME(0)%が必死に笑いを堪えていた。
					ELSE
						PRINTFORMW 何気無く横を見ると、必死に笑いを堪えている様子の%NTR_NAME(0)%が居た。
					ENDIF
					;ターゲットとあなたが一緒、ノーパン
				ELSEIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置 && CFLAG:LOCAL:服装_下半身下着２ == 0
					PRINTFORML なんらかの液体がこぼれ落ちたような音に%CALLNAME:MASTER%が視線をむけると
					PRINTFORMW %CALLNAME:LOCAL%が頬を染めてたまま音が発生した辺りを隠すかの様に立っている。
					;ターゲットとあなたが一緒
				ELSEIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置
					PRINTFORML %CALLNAME:LOCAL%は慌てたかのような表情をして、急に%SHOW_BOTTOM(LOCAL,1)%の下腹部を手で押さえた。
					PRINTFORMW 何故かは分からないが頬を染めて明らかに動揺しているようだ。
					;ターゲットと訪問者が一緒、一人だけ、ノーパン
				ELSEIF FLAG:訪問者の現在位置 == CFLAG:LOCAL:現在位置 && GET_N_WITH_VISITER(LOCAL) == 1 && CFLAG:LOCAL:服装_下半身下着２ == 0
					PRINTFORML 『こぽっ…』と音を立てて精液がこぼれ落ちた事に気づいた%NTR_NAME(0)%がその事を指摘すると
					PRINTFORM %CALLNAME:LOCAL%は
					;寝取られ
					IF TALENT:LOCAL:NTR
						PRINTFORMW 再び注ぎ込まれる事を期待するかのように%NTR_NAME(0)%に身を委ね始めた。
						;恋慕
					ELSEIF CFLAG:LOCAL:2 > 1000 && TALENT:LOCAL:恋慕
						PRINTFORMW 誰の所為かと責め立てて%NTR_NAME(0)%を睨み付けた。
						;その他
					ELSE
						PRINTW 涙ぐんで俯いてしまった。
					ENDIF
					;ターゲットと訪問者が一緒、一人だけ
				ELSEIF FLAG:訪問者の現在位置 == CFLAG:LOCAL:現在位置 && GET_N_WITH_VISITER(LOCAL) == 1
					PRINTFORML 先程注ぎ込んだと思われる精液が%CALLNAME:LOCAL%の足を伝っている事に気づいた%NTR_NAME(0)%がその事を指摘すると
					PRINTFORM %CALLNAME:LOCAL%は
					;寝取られ
					IF TALENT:LOCAL:NTR
						PRINTFORMW 再び注ぎ込まれる事を期待するかのように%NTR_NAME(0)%に身を委ねている。
						;恋慕
					ELSEIF CFLAG:LOCAL:2 > 1000 && TALENT:LOCAL:恋慕
						PRINTFORMW 誰の所為かと責め立てて%NTR_NAME(0)%を睨み付けた。
						;その他
					ELSE
						PRINTW 涙ぐんで俯いてしまった。
					ENDIF
					;ターゲットと訪問者が一緒、同じ場所に2人以上、ノーパン
				ELSEIF FLAG:訪問者の現在位置 == CFLAG:LOCAL:現在位置 && GET_N_WITH_VISITER(LOCAL) > 1 && CFLAG:LOCAL:服装_下半身下着２ == 0
					PRINTFORML %CALLNAME:LOCAL%の下腹部から『こぽっ…』と音を立てて精液がこぼれ落ちた事に気づいた%NTR_NAME(0)%は
					PRINTFORML 素知らぬ顔で他の住人に「何か液体が落ちる様な音がしなかったか」と聞き始めた。
					IF RAND:3 == 0
						PRINTFORMW 恥ずかしさに耐えられなかった%CALLNAME:LOCAL%が耳まで真っ赤にして俯いてしまった。
					ELSE
						PRINTFORMW 他の住人の手前、動揺を隠している様だが%CALLNAME:LOCAL%は頬を染めて体を震わせている。
					ENDIF
					;ターゲットと訪問者が一緒、同じ場所に2人以上
				ELSEIF FLAG:訪問者の現在位置 == CFLAG:LOCAL:現在位置 && GET_N_WITH_VISITER(LOCAL) > 1
					PRINTFORML %CALLNAME:LOCAL%は慌てたかのような表情をして、急に%SHOW_BOTTOM(LOCAL,1)%の下腹部を手で押さえた。
					PRINTFORML 頬を染めて明らかに動揺している様子に他の住人から心配されている。
					PRINTFORML 先程注ぎ込んだであろう精液が%CALLNAME:LOCAL%の足を伝い始めた事に気づいた%NTR_NAME(0)%は
					PRINTFORMW 何事も無かったかの様に振舞おうとする%CALLNAME:LOCAL%を他の住人からは見えない様に嗤っていた。
					;あなたと訪問者が居ない、他の住人が居る
				ELSEIF GET_N_WITH_VISITER(LOCAL) > 1
					PRINTFORML %CALLNAME:LOCAL%は慌てたかのような表情をして、急に%SHOW_BOTTOM(LOCAL,1)%の下腹部を手で押さえた。
					IF RAND:3 == 0
						PRINTFORMW 頬を染めて明らかに動揺している様子に他の住人から心配されている。
					ELSE
						PRINTFORMW しかし、すぐに何事も無かったかの様に振る舞ったようだ。
					ENDIF
					;誰も居ない
				ELSE
					PRINTFORMW %CALLNAME:LOCAL%は時間が経って溢れ出てきた精液を密かに拭き取って処理した。
				ENDIF
			ENDIF
		ENDIF
		;装着系は後で考える
		SIF LOCAL > 0 && !CFLAG:LOCAL:同室
			CONTINUE
		;-------------------------------------------------
		;主人、同性、調教者の能力の処理
		;-------------------------------------------------
		CALL SOURCE_SEX_CHECK(LOCAL)
		CALL PLAYER_SKILL_CHECK(LOCAL)
		;-------------------------------------------------
		;技巧、好感度、ムード、理性のチェック
		;-------------------------------------------------
		FOR LOCAL:1,0,100
			SOURCE:LOCAL:(LOCAL:1) = SOURCE:LOCAL:(LOCAL:1) *  MASTER_FAVOR_CHECK(LOCAL,LOCAL:1) * TECHNIQUE_CHECK(LOCAL,LOCAL:1) / 10000
			SOURCE:LOCAL:(LOCAL:1) = SOURCE:LOCAL:(LOCAL:1) *  MOOD_CHECK(LOCAL,LOCAL:1) * REASON_CHECK(LOCAL,LOCAL:1) / 10000
		NEXT
		;-------------------------------------------------
		;中毒充足のチェック
		;-------------------------------------------------
		;自慰中毒
		IF EXP_UP(22,LOCAL)
			FOR LOCAL:1,0,100
				;快感,性行動,欲情,恭順,露出,屈従
				SIF (LOCAL:1 >= 0 && LOCAL:1 <= 9) || LOCAL:1 == 11 || (LOCAL:1 >= 15 && LOCAL:1 <= 18) 
					SOURCE:LOCAL:(LOCAL:1) = SOURCE:LOCAL:(LOCAL:1) * (100 + GET_REVISION(ABL:LOCAL:自慰中毒, 500, 15)) / 100
			NEXT
		ENDIF
		;-------------------------------------------------
		;バイブなど付けっぱなしアイテムのチェック
		;-------------------------------------------------
		CALL EQUIP_CHECK(LOCAL)
		;-------------------------------------------------
		;経験加算
		;-------------------------------------------------
		CALL SOURCE_EXP(LOCAL)
		;-------------------------------------------------
		;処女、童貞喪失の処理
		;-------------------------------------------------
		CALL LOST_VIRGIN(LOCAL)
		;-------------------------------------------------
		;快Ｃのソース
		;-------------------------------------------------
		CALL SOURCE_快Ｃ(LOCAL)
		;-------------------------------------------------
		;快Ｖのソース
		;-------------------------------------------------
		CALL SOURCE_快Ｖ(LOCAL)
		;-------------------------------------------------
		;快Ａのソース
		;-------------------------------------------------
		CALL SOURCE_快Ａ(LOCAL)
		;-------------------------------------------------
		;快Ｂのソース
		;-------------------------------------------------
		CALL SOURCE_快Ｂ(LOCAL)
		;-------------------------------------------------
		;与快Ｃのソース
		;-------------------------------------------------
		SIF !(HAS_PENIS(PLAYER))
			CALL SOURCE_与快Ｃ(LOCAL)
		;-------------------------------------------------
		;与快Ｖのソース
		;-------------------------------------------------
		CALL SOURCE_与快Ｖ(LOCAL)
		;-------------------------------------------------
		;与快Ａのソース
		;-------------------------------------------------
		CALL SOURCE_与快Ａ(LOCAL)
		;-------------------------------------------------
		;与快Ｂのソース
		;-------------------------------------------------
		CALL SOURCE_与快Ｂ(LOCAL)
	NEXT
	;童貞喪失
	IF !TALENT:PLAYER:1
		SELECTCASE SELECTCOM
			CASE 60,61,62,63,65,66,67,68,69,70
				PRINTW 童貞喪失
		ENDSELECT
	ENDIF
	IF !TALENT:TARGET:1
		SELECTCASE SELECTCOM
			CASE 64,92,93,94,71
				PRINTFORMW 童貞喪失(%CALLNAME:TARGET%)
		ENDSELECT
	ENDIF
	;-------------------------------------------------
	;同じコマンドの連続実行による上下の処理（快楽系）
	;-------------------------------------------------
	IF SELECTCOM == PREVCOM && TFLAG:50 == TFLAG:151 && (SELECTCOM < 350 || SELECTCOM >= 500)
		;UP:0,1,2,3は絶頂に絡むので先に処理してある
		CUP:TARGET:快Ｃ /= 2
		CUP:TARGET:快Ｖ /= 2
		CUP:TARGET:快Ａ /= 2
		CUP:TARGET:快Ｂ /= 2
	ENDIF
	FOR LOCAL,0,CHARANUM
		;装着系は後で考える
		SIF LOCAL > 0 && !CFLAG:LOCAL:同室
			CONTINUE
		CALL SOURCE_CVAB_EXTRA(LOCAL)
		BASE:LOCAL:勃起 = BASE:LOCAL:勃起 + CUP:LOCAL:快Ｃ / 5 + CUP:TARGET:快Ｖ / 10 + CUP:TARGET:快Ａ / 10 + CUP:TARGET:快Ｂ / 10
		;勃起ペナルティ判定
		CALL SOJI_SexHara_ErectPunishment(LOCAL)
		;-------------------------------------------------
		;絶頂処理
		;-------------------------------------------------
		CALL ORGASM_ADD(LOCAL)
	NEXT
	;-------------------------------------------------
	;射精チェック（ふたなり・オトコ）
	;-------------------------------------------------
	;要検討
	CALL EVENT_SHOOT
	FOR LOCAL,0,CHARANUM
		;装着系は後で考える
		SIF LOCAL > 0 && !CFLAG:LOCAL:同室
			CONTINUE
		;-------------------------------------------------
		;調教対象の噴乳チェック
		;-------------------------------------------------
		CALL TARGET_MILK_CHECK(LOCAL)
		;-------------------------------------------------
		;中毒充足チェック
		;-------------------------------------------------
		;精液中毒
		IF EXP_UP(14,LOCAL)
			LOCAL:1 = 100 + GET_REVISION(ABL:LOCAL:精液中毒 * EXP_UP(14,LOCAL), 500, 30)
			SOURCE:LOCAL:情愛 = SOURCE:LOCAL:情愛 * LOCAL:1 / 100
			SOURCE:LOCAL:性行動 = SOURCE:LOCAL:性行動 * LOCAL:1 / 100
			SOURCE:LOCAL:達成 = SOURCE:LOCAL:達成 * LOCAL:1 / 100
			SOURCE:LOCAL:欲情 = SOURCE:LOCAL:欲情 * LOCAL:1 / 100
			SOURCE:LOCAL:恭順 = SOURCE:LOCAL:恭順 * LOCAL:1 / 100
			SOURCE:LOCAL:屈従 = SOURCE:LOCAL:屈従 * LOCAL:1 / 100
		ENDIF
		;-------------------------------------------------
		;情愛のソース
		;-------------------------------------------------
		CALL SOURCE_情愛(LOCAL)
		;-------------------------------------------------
		;性行動のソース
		;-------------------------------------------------
		CALL SOURCE_性行動(LOCAL)
		;-------------------------------------------------
		;達成のソース
		;-------------------------------------------------
		CALL SOURCE_達成(LOCAL)
		;-------------------------------------------------
		;痛みのソース
		;-------------------------------------------------
		CALL SOURCE_苦痛(LOCAL)
		;-------------------------------------------------
		;恐れのソース
		;-------------------------------------------------
		CALL SOURCE_恐怖(LOCAL)
		;-------------------------------------------------
		;液体のソース
		;-------------------------------------------------
		CALL SOURCE_液体(LOCAL)
		;-------------------------------------------------
		;欲情のソース
		;-------------------------------------------------
		CALL SOURCE_欲情(LOCAL)
		;-------------------------------------------------
		;恭順のソース
		;-------------------------------------------------
		CALL SOURCE_恭順(LOCAL)
		;-------------------------------------------------
		;露出のソース
		;-------------------------------------------------
		CALL SOURCE_露出(LOCAL)
		;-------------------------------------------------
		;屈従のソース
		;-------------------------------------------------
		CALL SOURCE_屈従(LOCAL)
		;-------------------------------------------------
		;歓楽のソース
		;-------------------------------------------------
		CALL SOURCE_歓楽(LOCAL)
		;-------------------------------------------------
		;征服のソース
		;-------------------------------------------------
		CALL SOURCE_征服(LOCAL)
		;-------------------------------------------------
		;受動のソース
		;-------------------------------------------------
		CALL SOURCE_受動(LOCAL)
		;-------------------------------------------------
		;誘惑のソース
		;-------------------------------------------------
		CALL SOURCE_誘惑(LOCAL)
		;-------------------------------------------------
		;辱めのソース
		;-------------------------------------------------
		CALL SOURCE_辱め(LOCAL)
		;-------------------------------------------------
		;挑発のソース
		;-------------------------------------------------
		CALL SOURCE_挑発(LOCAL)
		;-------------------------------------------------
		;奉仕のソース
		;-------------------------------------------------
		CALL SOURCE_奉仕(LOCAL)
		;-------------------------------------------------
		;強要のソース
		;-------------------------------------------------
		CALL SOURCE_強要(LOCAL)
		;-------------------------------------------------
		;加虐のソース
		;-------------------------------------------------
		CALL SOURCE_加虐(LOCAL)
		;-------------------------------------------------
		;不潔のソース
		;-------------------------------------------------
		CALL SOURCE_不潔(LOCAL)
		;-------------------------------------------------
		;鬱屈のソース
		;-------------------------------------------------
		CALL SOURCE_鬱屈(LOCAL)
		;-------------------------------------------------
		;逸脱のソース
		;-------------------------------------------------
		CALL SOURCE_逸脱(LOCAL)
		;-------------------------------------------------
		;反感のソース
		;-------------------------------------------------
		CALL SOURCE_反感(LOCAL)
		;-------------------------------------------------
		;素質などによる上下の処理
		;-------------------------------------------------
		CALL SOURCE_EXTRA(LOCAL)
	NEXT
	;-------------------------------------------------
	;同じコマンドの連続実行による上下の処理
	;-------------------------------------------------
	IF SELECTCOM == PREVCOM && TFLAG:50 == TFLAG:151 && (SELECTCOM < 350 || SELECTCOM >= 500)
		;UP:0,1,2は絶頂に絡むので先に処理してある
		UP:欲情 /= 2
		UP:恭順 /= 2
		UP:屈服 /= 2
		UP:習得 /= 2
		UP:恥情 /= 2
		UP:恐怖 /= 2
		UP:苦痛 /= 2
		UP:好意 /= 2
		UP:優越 /= 2
	ENDIF
	LOCAL:1 = 0
	FOR LOCAL,0,CHARANUM
		LOCAL:1 += CUP:LOCAL:欲情 / 5
		LOCAL:1 += MAX(5,(ABL:LOCAL:マゾっ気 - 3)) * (CUP:LOCAL:苦痛 + CUP:LOCAL:苦痛) / 2
		LOCAL:1 += MAX(5,(ABL:LOCAL:マゾっ気 - 1)) * CUP:LOCAL:恥情 / 2
		LOCAL:1 = MIN(500,LOCAL:1)
		BASE:LOCAL:勃起 = BASE:LOCAL:勃起 + LOCAL:1
		;勃起ペナルティ判定
		CALL SOJI_SexHara_ErectPunishment(LOCAL)
		;装着系は後で考える
		SIF LOCAL > 0 && !CFLAG:LOCAL:同室
			CONTINUE
		;-------------------------------------------------
		;体力・気力の減少
		;-------------------------------------------------
		CALL SOURCE_DOWNBASE(LOCAL)
		;-------------------------------------------------
		;苦痛快楽経験、奉仕快楽経験のチェック
		;-------------------------------------------------
		CALL EXP_GOT_CHECK(LOCAL)
	NEXT
	$SKIP
	;-------------------------------------------------
	;調教口上
	;-------------------------------------------------
	CALL KOJO_MESSAGE_COM
	IF TARGET
		DRAWLINE
		FOR LOCAL,1,CHARANUM
			SIF TCVAR:(TARGET:LOCAL):30 > 1
				CONTINUE
			SIF TCVAR:(TARGET:LOCAL):20
				CALL KOJO_MESSAGE_COUNTER(TARGET:LOCAL)
		NEXT
	ENDIF
	;-------------------------------------------------
	;EXP取得表示
	;-------------------------------------------------
	LOCAL:2 = 0
	LOCAL:3 = 0
	FOR LOCAL,0,CHARANUM
		FOR LOCAL:1,0,100
			SIF TCVAR:LOCAL:表示カット
				CONTINUE
			IF EXP_UP(LOCAL:1,LOCAL)
				LOCAL:2 ++
				LOCAL:3 ++
				SIF LOCAL:2 == 1
					PRINTL
				PRINTFORML %EXPNAME:(LOCAL:1)% +{EXP_UP(LOCAL:1,LOCAL)}(%CALLNAME:LOCAL%)
			ENDIF
		NEXT
		LOCAL:2 = 0
	NEXT
	SIF LOCAL:3
		WAIT
	;-------------------------------------------------
	;ABLUP
	;-------------------------------------------------
	FOR LOCAL,0,CHARANUM
		SIF TCVAR:LOCAL:表示カット
			CONTINUE
		CALL SOURCE_ABLUP(LOCAL)
	NEXT
	;-------------------------------------------------
	;触手の射精時の口上
	;-------------------------------------------------
	CALL KOJO_MESSAGE_PALAMCNG_E
	;-------------------------------------------------
	;奴隷の絶頂時の口上
	;-------------------------------------------------
	CALL MESSAGE_PALAMCNG_B2
	CALL KOJO_MESSAGE_PALAMCNG_B
	;-------------------------------------------------
	;調教者の射精時の口上
	;-------------------------------------------------
	IF NOWEX:MASTER:11
		SIF TCVAR:MASTER:12 == 1
			PRINT 膣内
		SIF NOWEX:MASTER:11 > 1
			PRINT 大量
		PRINTFORML 射精
	ENDIF
	IF NOWEX:TARGET:11
		SIF TCVAR:TARGET:12 == 1
			PRINT 膣内
		SIF NOWEX:TARGET:11 > 1
			PRINT 大量
		PRINTFORML 射精(%CALLNAME:TARGET%)
	ENDIF
	CALL KOJO_MESSAGE_PALAMCNG_A
	;-------------------------------------------------
	;調教後の口上
	;-------------------------------------------------
	CALL KOJO_MESSAGE_PALAMCNG_C
	;-------------------------------------------------
	;パラメータ変動による口上
	;-------------------------------------------------
	CALL KOJO_MESSAGE_PALAMCNG_D
	;-------------------------------------------------
	;刻印取得のチェック
	;-------------------------------------------------
	CALL MARK_GOT_CHECK(TARGET)
	;-------------------------------------------------
	;刻印取得による口上
	;-------------------------------------------------
	CALL KOJO_MESSAGE_MARKCNG
	;-------------------------------------------------
	;従順上昇による[抵抗]の消滅チェック
	;-------------------------------------------------
	CALL JUJUN_UP_CHECK
	;-------------------------------------------------
	;調教ソースの表示
	;-------------------------------------------------
	CALL SHOW_SOURCE
	;-------------------------------------------------
	;数値変動の表示
	;-------------------------------------------------
	;相性
	R = NO:PLAYER
	SIF RELATION:R != 0
		PRINTFORML ＜相性{RELATION:R/100}.{RELATION:R%100}倍＞
	SIF SELECTCOM == PREVCOM && TFLAG:50 == TFLAG:151 && (SELECTCOM < 350 || SELECTCOM >= 500)
		PRINTL ＜同一コマンド連続実行＞
	;気力０
	SIF BASE:1 <= 0
		PRINTL ★気力０★
	LOCAL:1 = 0
	FOR LOCAL,0,CHARANUM
		IF DOWNBASE:LOCAL:体力
			PRINTFORM 体力-{DOWNBASE:LOCAL:体力} 
			LOCAL:1 ++
		ENDIF
		IF DOWNBASE:LOCAL:気力
			PRINTFORM 気力-{DOWNBASE:LOCAL:気力} 
			LOCAL:1 ++
		ENDIF
		SIF LOCAL:1
			PRINTFORML (%CALLNAME:LOCAL%)
		LOCAL:1 = 0
	NEXT
	PRINTL 
	;-------------------------------------------------
	;パラメータの上昇＆表示（DOWNもここで）
	;-------------------------------------------------
	VARSET LOCAL
	FOR LOCAL:1,0,CHARANUM
		SIF TCVAR:(LOCAL:1):表示カット
			CONTINUE
		LOCAL:3 = 0
		FOR LOCAL,0,100
			SIF PALAMNAME:LOCAL == ""
				CONTINUE
			SIF CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL == 0
				CONTINUE
			IF !LOCAL:3
				PRINTL
				PRINTFORML %CALLNAME:(LOCAL:1)%
			ENDIF
			LOCAL:3 ++
			LOCAL:2 = CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL
			PRINTFORML %PALAMNAME:LOCAL,8,LEFT% {PALAM:(LOCAL:1):LOCAL,10,LEFT} + {CUP:(LOCAL:1):LOCAL,10,LEFT} =   {PALAM:(LOCAL:1):LOCAL + CUP:(LOCAL:1):LOCAL,10,LEFT}
			PALAM:(LOCAL:1):LOCAL += CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL
			CUP:(LOCAL:1):LOCAL = 0
			CDOWN:(LOCAL:1):LOCAL = 0
		NEXT
	NEXT
	;-------------------------------------------------
	;好感度上昇
	;-------------------------------------------------
	CALL FAVOR_CALC(TARGET)
	;NTR貞操帯カウンタ処理
	IF FLAG:貞操帯鍵購入フラグ
		FLAG:貞操帯鍵有効カウンタ = MAX(FLAG:貞操帯鍵有効カウンタ - TIME_PROGRESS(TFLAG:300),0)
		IF FLAG:貞操帯鍵有効カウンタ == 0
			PRINTFORMW 購入した%CALLNAME:(FLAG:貞操帯鍵購入フラグ)%貞操帯の鍵の有効期限が切れた
			IF CFLAG:(FLAG:貞操帯鍵購入フラグ):うふふ>0 && CFLAG:MASTER:うふふ>0
				;うふふ中に切れた場合
				TRYCALLLIST
					FUNC NTR_MESSAGE_COM_K{FLAG:貞操帯鍵購入フラグ}_350_0_3
					FUNC NTR_MESSAGE_COM_K_350_0_3(FLAG:貞操帯鍵購入フラグ)
				ENDFUNC
				IF TALENT:(FLAG:貞操帯鍵購入フラグ):公衆便所
					CFLAG:(FLAG:貞操帯鍵購入フラグ):うふふ = 0
					CFLAG:MASTER:うふふ = 0
				ENDIF
			ENDIF
			FLAG:貞操帯鍵購入フラグ = 0
		ENDIF
	ENDIF

;-------------------------------------------------
;調教ソースの表示（DOWNもここで）
;-------------------------------------------------
@SHOW_SOURCE
	SIF TCVAR:表示カット
		RETURN 0
	;PRINTL 　
	SIF SOURCE:快Ｃ > 0
		PRINTFORM 快Ｃ({SOURCE:快Ｃ})
	SIF SOURCE:快Ｖ > 0
		PRINTFORM 快Ｖ({SOURCE:快Ｖ})
	SIF SOURCE:快Ａ > 0
		PRINTFORM 快Ａ({SOURCE:快Ａ})
	SIF SOURCE:快Ｂ > 0
		PRINTFORM 快Ｂ({SOURCE:快Ｂ})
	SIF SOURCE:情愛 > 0
		PRINTFORM 情愛({SOURCE:情愛})
	SIF SOURCE:性行動 > 0
		PRINTFORM 性行動({SOURCE:性行動})
	SIF SOURCE:達成 > 0
		PRINTFORM 達成感({SOURCE:達成})
	SIF SOURCE:苦痛 > 0
		PRINTFORM 痛み({SOURCE:苦痛})
	SIF SOURCE:恐怖 > 0
		PRINTFORM 恐れ({SOURCE:恐怖})
	SIF SOURCE:液体 > 0
		PRINTFORM 液体({SOURCE:液体})
	SIF SOURCE:欲情 > 0
		PRINTFORM 欲情({SOURCE:欲情})
	SIF SOURCE:恭順 > 0
		PRINTFORM 恭順({SOURCE:恭順})
	SIF SOURCE:露出 > 0
		PRINTFORM 露出({SOURCE:露出})
	SIF SOURCE:屈従 > 0
		PRINTFORM 屈服({SOURCE:屈従})
	SIF SOURCE:歓楽 > 0
		PRINTFORM 歓楽({SOURCE:歓楽})
	SIF SOURCE:征服 > 0
		PRINTFORM 征服({SOURCE:征服})
	SIF SOURCE:受動 > 0
		PRINTFORM 受動({SOURCE:受動})
	SIF SOURCE:不潔 > 0
		PRINTFORM 不潔({SOURCE:不潔})
	SIF SOURCE:鬱屈 > 0
		PRINTFORM 鬱屈({SOURCE:鬱屈})
	SIF SOURCE:逸脱 > 0
		PRINTFORM 逸脱({SOURCE:逸脱})
	SIF SOURCE:反感 > 0
		PRINTFORM 反発({SOURCE:反感})
	PRINTL 　

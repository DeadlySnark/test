@EVENTCOM
;ターン終了の処理
@EVENTCOMEND
CALL TURN_RESET

;パンツ没収、収集
IF TFLAG:102 < 2
	IF TARGET > 0 && !CFLAG:睡眠
		IF STRLENS(CSTR:MASTER:12) && RAND:2
			PRINTL 
			PRINTFORMW %CALLNAME:TARGET%に見とがめられ身体検査をされた…
			PRINTFORMW 
			PRINTFORMW 
			PRINTFORMW 
			PRINTFORMW 収集品を没収された
			SIF !GETBIT (CFLAG:10 ,0)
				PRINTFORMW %CALLNAME:TARGET%に弱みを握られた
			SETBIT CFLAG:10 ,0
			CALL KOJO_EVENT(11, TARGET, 0)
			CSTR:MASTER:12 =
		ELSEIF FLAG:貞操帯鍵購入フラグ && RAND:2 && CFLAG:TARGET:NTR貞操帯反応
			;NTR貞操帯鍵
			PRINTL 
			PRINTFORMW %CALLNAME:TARGET%に見とがめられ身体検査をされた…
			PRINTFORMW 
			PRINTFORMW 
			PRINTFORMW 
			PRINTFORMW 貞操帯の鍵を発見された。
			CALL NTR_LOCK_REACTION(TARGET)
		ENDIF
	ENDIF
ENDIF


IF BASE:MASTER:体力 <= 500
	PRINTW （体力が限界に来ています　一日を終了します）
	CFLAG:MASTER:睡眠 = 1
	CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME
	IF TARGET > 0 && TALENT:恋慕 && BASE:体力 >= 500
		PRINTFORMW %CALLNAME%が%CALLNAME:MASTER%を私室へと運んでくれた…
		CFLAG:MASTER:現在位置 = CFLAG:MASTER:開始位置
		CFLAG:現在位置 = CFLAG:MASTER:開始位置
		CALL KOJO_EVENT(7, TARGET, 5)
	ENDIF
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:うふふ = 0
	NEXT
	BEGIN AFTERTRAIN
ENDIF
IF TARGET && CFLAG:不機嫌
	PRINTFORMW %CALLNAME:TARGET%を怒らせてしまった…
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:うふふ = 0
	NEXT
ENDIF
IF TARGET && BASE:体力 < 500
	PRINTFORMW （%CALLNAME:TARGET%の体力が限界に来ています）
	CFLAG:TARGET:衰弱 = 360
	CALL CHARA_SLEEP, TARGET
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:うふふ = 0
	NEXT
ENDIF

@TURN_RESET
;処女喪失
FOR LOCAL,0,CHARANUM
	IF TCVAR:LOCAL:破瓜
		TRYCALL COM417_Lost_Virgin(LOCAL,TEQUIP:LOCAL:Ｖセックス)
		TALENT:LOCAL:処女 = 0
	ENDIF
NEXT
;童貞喪失
SELECTCASE SELECTCOM
	CASE 60,61,65,67,68
		TALENT:MASTER:1 |= 1
	CASE 62,63,66,69,70
		TALENT:MASTER:1 |= 2
ENDSELECT
SELECTCASE SELECTCOM
	CASE 64
		TALENT:1 |= 1
	CASE 92,93,94
		TALENT:1 |= 2
	CASE 71
		IF MASTER_POSE(6,1,1)
			TALENT:1 |= 2
		ELSE
			TALENT:1 |= 1
		ENDIF
ENDSELECT

;既成事実
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:うふふ
		SETBIT CFLAG:LOCAL:既成事実 , 1
NEXT
;告白成功
SIF SELECTCOM == 352 && TFLAG:193 > 0
		SETBIT CFLAG:1 , 0

;食事
SIF SELECTCOM == 414 || SELECTCOM == 415
	FLAG:料理 = 0
;脱衣コマンド
SELECTCASE SELECTCOM
	;上半身脱衣
	CASE 200
		EQUIP:上半身上着１ = 0
		EQUIP:上半身上着２ = 0
		EQUIP:ボディースーツ = 0
		EQUIP:ワンピース = 0
		EQUIP:着物 = 0
		EQUIP:レオタード = 0
		CALL CLOTHES_SETTING_TRAIN(TARGET)
	;下半身脱衣
	CASE 201
		EQUIP:下半身上着１ = 0
		EQUIP:下半身上着２ = 0
		EQUIP:スカート = 0
		CALL CLOTHES_SETTING_TRAIN(TARGET)
	;上半身下着脱衣
	CASE 202
		EQUIP:上半身下着１ = 0
		EQUIP:上半身下着２ = 0
		CALL CLOTHES_SETTING_TRAIN(TARGET)
	;下半身下着脱衣
	CASE 203
		EQUIP:下半身下着１ = 0
		EQUIP:下半身下着２ = 0
		CALL CLOTHES_SETTING_TRAIN(TARGET)
ENDSELECT

;調教履歴を1ターン保存
FOR LOCAL,0,CHARANUM
	TCVAR:LOCAL:110 = TCVAR:LOCAL:10
	TCVAR:LOCAL:112 = TCVAR:LOCAL:12
	
	SIF TCVAR:LOCAL:102
		TCVAR:LOCAL:102 --
	SIF !TCVAR:LOCAL:102
		TCVAR:LOCAL:103 = -1
	SIF TCVAR:LOCAL:104
		TCVAR:LOCAL:104 --
	SIF !TCVAR:LOCAL:104
		TCVAR:LOCAL:105 = -1
	;体位の保存
	FOR LOCAL:1,1,11
		TEQUIP:LOCAL:(110 + LOCAL:1) = TEQUIP:LOCAL:(100 + LOCAL:1)
	NEXT
	;体勢制御
	IF TCVAR:LOCAL:体勢制御
		TCVAR:LOCAL:体勢制御 --
		SIF TCVAR:LOCAL:体勢制御 == 0
			TCVAR:LOCAL:体勢 = 0
	ENDIF
	;位置関係制御
	IF TCVAR:LOCAL:位置関係制御
		TCVAR:LOCAL:位置関係制御 --
		SIF TCVAR:LOCAL:位置関係制御 == 0
			TCVAR:LOCAL:位置関係 = 0
	ENDIF
	;ついでに射精処理用文字列変数の初期化
	CSTR:LOCAL:10 = /
	CSTR:LOCAL:11 = /
	;服装フラグ
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
NEXT
;BASEの変動
FOR LOCAL,0,CHARANUM
	;勃起度
	;加算後
	CFLAG:LOCAL:勃起度２ = BASE:LOCAL:勃起
	BASE:LOCAL:勃起 = MIN(MAXBASE:LOCAL:勃起,BASE:LOCAL:勃起)
	IF NOWEX:LOCAL:11
		IF !MASTER_POSE(6,1) && !MASTER_POSE(7,1)
			BASE:LOCAL:勃起 = BASE:LOCAL:勃起 * (ABL:LOCAL:欲望 + 1) * (BASE:LOCAL:精力 + 200) / ((MAXBASE:LOCAL:精力 + 200) * (ABL:LOCAL:欲望 + 10))
		ELSE
			BASE:LOCAL:勃起 = 1000
		ENDIF
		;勃起ペナルティ判定
		CALL SOJI_SexHara_ErectPunishment(LOCAL)
	ENDIF
NEXT

;フラグのリセット
TFLAG:193 = 0
TFLAG:194 = 0
TSTR:2 = 

;TCVARのリセット
FOR LOCAL,0,100
	FOR LOCAL:1,0,CHARANUM
		TCVAR:(LOCAL:1):LOCAL = 0
	NEXT
NEXT
;挿入履歴
FOR LOCAL,0,CHARANUM
	TCVAR:LOCAL:100 = TEQUIP:LOCAL:Ｖセックス
	TCVAR:LOCAL:101 = TEQUIP:LOCAL:Ａセックス
NEXT

TFLAG:110 = TCVAR:MASTER:130

;ビデオテープの残量
SIF TEQUIP:ビデオ撮影
	TEQUIP:ビデオ撮影 --


;FIRSTTIMEにSELECTCOMをぶっこむ
CALLF FIRSTTIME(SELECTCOM)
FOR LOCAL,1,CHARANUM
	SIF EXP_UP(15,LOCAL)
		CALLF FIRSTTIME("精飲",0,LOCAL)
NEXT
;射精箇所
FOR LOCAL:1,0,60
	IF GETBIT(TFLAG:1,LOCAL:1)
		LOCALS = "射精"{LOCAL:1}
		CALLF FIRSTTIME(LOCALS,0,TARGET)
	ENDIF
NEXT
PREVCOM = SELECTCOM
;派生コマンドの履歴
TFLAG:151 = TFLAG:50
;前回のTARGET
TFLAG:103 = TARGET
TFLAG:150 = ASSIPLAY

;経験値取表示用
FOR LOCAL,0,CHARANUM
	;現在位置の保存
	CFLAG:LOCAL:前ターン位置 = CFLAG:LOCAL:現在位置
	FOR LOCAL:1,0,100
		TCVAR:LOCAL:(400 + LOCAL:1) = EXP:LOCAL:(LOCAL:1)
	NEXT
NEXT


;TFLAGのリセット
REPEAT 100
	TFLAG:COUNT = 0
REND

;眠いと気力が減少していく
IF TIME_PROGRESS(CFLAG:MASTER:起床時間) >= 1260 && !CFLAG:MASTER:睡眠
	BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 3 * TIME_PROGRESS(TFLAG:300) , 0)
	BASE:MASTER:気力 = MAX(BASE:MASTER:気力 - 5 * TIME_PROGRESS(TFLAG:300) , 0)
ENDIF


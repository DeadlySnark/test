;-------------------------------------------------
;撃破する
; バランスとかいい加減なので、適当に修正してね。
;-------------------------------------------------

;---------------------------------------------
;コマンド実行
@COM417
	CALL COM417_Setup(MASTER)
	CALL COM417_Battle(MASTER,RESULT:0,RESULT:1)
	CALL COM417_Cleanup(MASTER,RESULT:0)
RETURN 0


;---------------------------------------------
;戦闘前準備
;@param ARG 訪問者に対峙するキャラクター番号
;@return あなた陣営戦力,訪問者陣営戦力
;分岐にTFLAG:99を使用
@COM417_Setup(ARG)
	VARSET LOCAL
	LOCAL:1 = ABL:ARG:戦闘能力 + 1
	PRINTFORML %CALLNAME:ARG%の戦力:+{LOCAL:1}
	;あなたより弱い訪問者は存在しない。いいね？
	IF ABL:MASTER:42 < 5
	;と言ったな。あれは嘘だ。
	ELSEIF ABL:MASTER:42 <= 15
		PRINTFORML 日頃の鍛錬の成果を見せますか？
		PRINTL [0] いつ見せる？今でしょ！
		PRINTL [1] 今はその時ではない
		$INPUT_LOOP_BATTELESUPERMODE01
		INPUT
		IF RESULT == 0
			TFLAG:99 = 1
		ELSEIF RESULT == 1
			
		ELSE
		GOTO INPUT_LOOP_BATTELESUPERMODE01
		ENDIF
	;猛者共がひしめく幻想郷で生き抜いた、筋肉モリモリマッチョマンの変態(という名の紳士)だ
	ELSE
		PRINTFORML 鍛錬の果てに修得した、体の内の荒ぶる力を開放しますか？
		PRINTL [0]開放しない
		PRINTL [1]ちょっとだけ本気出す
		PRINTL [2]全力を出す
		$INPUT_LOOP_BATTELESUPERMODE02
		INPUT
		IF RESULT == 0

		ELSEIF RESULT == 1
			TFLAG:99 = 1
		ELSEIF RESULT == 2
			TFLAG:99 = 2
		ELSE
			GOTO INPUT_LOOP_BATTELESUPERMODE02
		ENDIF
	ENDIF
	;本気度の度合いによる戦闘力の分岐
	IF TFLAG:99 == 0
		LOCAL:2 = LOCAL:1 + 3 + RAND:4
		PRINTFORML %NTR_NAME(0)%の戦力:+{LOCAL:2}
	ELSEIF TFLAG:99 == 1
		LOCAL:2 = LOCAL:1 - 2 + RAND:4
		PRINTFORML %NTR_NAME(0)%の戦力:+{LOCAL:2}
	ELSEIF TFLAG:99 == 2
		LOCAL:2 = LOCAL:1 - 10 + RAND:4
		PRINTFORML %NTR_NAME(0)%の戦力:+{LOCAL:2}
	ENDIF
	
	PRINTFORMW %CALLNAME:ARG%は%NTR_NAME(0)%を追い出すべく、戦闘を開始した！
	FOR LOCAL,0,CHARANUM
		;同行状態は強制解除(バグ防止)
		CFLAG:LOCAL:同行 = 0
		IF LOCAL == ARG
			;自分自身は参戦できない
		ELSEIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置
			;同じ位置にいれば参戦してくれる
			SELECTCASE COM417_Position(LOCAL)
				CASE IS <= -50
					PRINTFORML %CALLNAME:LOCAL%は全力で%NTR_NAME(0)%を援護した(戦力+{ABL:LOCAL:戦闘能力})
					LOCAL:2 += ABL:LOCAL:戦闘能力
				CASE -49 TO -20
					PRINTFORML %CALLNAME:LOCAL%は積極的に%NTR_NAME(0)%を援護した(戦力+{ABL:LOCAL:戦闘能力/2})
					LOCAL:2 += ABL:LOCAL:戦闘能力/2
				CASE -19 TO -1
					PRINTFORML %CALLNAME:LOCAL%はさりげなく%NTR_NAME(0)%を援護した(戦力+{ABL:LOCAL:戦闘能力/3})
					LOCAL:2 += ABL:LOCAL:戦闘能力/3
				CASE 0 TO 19
					PRINTFORML %CALLNAME:LOCAL%はさりげなく%CALLNAME:ARG%を援護した(戦力+{ABL:LOCAL:戦闘能力/3})
					LOCAL:1 += ABL:LOCAL:戦闘能力/3
				CASE 20 TO 49
					PRINTFORML %CALLNAME:LOCAL%は積極的に%CALLNAME:ARG%を援護した(戦力+{ABL:LOCAL:戦闘能力/2})
					LOCAL:1 += ABL:LOCAL:戦闘能力/2
				CASE IS >= 50
					PRINTFORML %CALLNAME:LOCAL%は全力で%CALLNAME:ARG%を援護した(戦力+{ABL:LOCAL:戦闘能力})
					LOCAL:1 += ABL:LOCAL:戦闘能力
				CASEELSE
					;上記の選択肢ならありえないが一応表示
					PRINTFORML %CALLNAME:LOCAL%はどちらの味方をしていいのかわからないようだ……。
			ENDSELECT
		ENDIF
	NEXT
RETURN LOCAL:1,LOCAL:2


;---------------------------------------------
;戦闘中処理 1ターン10分
;@param ARG   訪問者に対峙するキャラクター番号
;@param ARG:1 あなた陣営戦力
;@param ARG:2 訪問者陣営戦力
;@return 勝った場合1、負けた場合0
@COM417_Battle(ARG,ARG:1,ARG:2)
	VARSET LOCAL
	LOCAL:0 = 1
	LOCAL:1 = 0
	LOCAL:2 = ARG:1
	LOCAL:3 = ARG:2

	WHILE LOCAL:0

		LOCAL:1++
		TIME += 10

		PRINTFORMW {LOCAL:1}ターン目 %CALLNAME:ARG%側の戦力 {LOCAL:2} : %NTR_NAME(0)%側の戦力 {LOCAL:3}
		
		PRINTFORMW ピチューン　　・
		PRINTFORMW 　　　　　　　・
		PRINTFORMW 　　　　　　　・　　ピチューン
		
		LOCAL:4 = LOCAL:2+RAND:5
		LOCAL:5 = LOCAL:3+RAND:5
		
		SELECTCASE LOCAL:4
			CASE IS < LOCAL:5
				;判定に敗北
				LOCAL:7 = SQRT(POWER(LOCAL:5,2)-POWER(LOCAL:4,2))
				PRINTFORML %CALLNAME:ARG%側は戦力に{LOCAL:7}のダメージを受けた！
				LOCAL:2 -= LOCAL:7
				PRINTFORMW (%CALLNAME:ARG%の体力-{500*LOCAL:7/ARG:1}、気力-{500*LOCAL:7/ARG:1})
				BASE:ARG:体力 = MAX(BASE:ARG:体力 - 500*LOCAL:7/ARG:1,500)
				BASE:ARG:気力 = MAX(BASE:ARG:気力 - 500*LOCAL:7/ARG:1,100)
			CASE IS > LOCAL:5
				;判定に勝利
				LOCAL:7 = SQRT(POWER(LOCAL:4,2)-POWER(LOCAL:5,2))
				PRINTFORML %NTR_NAME(0)%側の戦力に{LOCAL:7}のダメージを与えた！
				LOCAL:3 -= LOCAL:7
				PRINTFORMW (%CALLNAME:ARG%の体力-{150*LOCAL:7/ARG:1}、気力-{150*LOCAL:7/ARG:1})
				BASE:ARG:体力 = MAX(BASE:ARG:体力-150*LOCAL:7/ARG:1,500)
				BASE:ARG:気力 = MAX(BASE:ARG:気力-150*LOCAL:7/ARG:1,100)
			CASEELSE
				PRINTFORML どちらも致命的なダメージは受けていないようだ
				PRINTFORMW (%CALLNAME:ARG%の体力-{150*LOCAL:7/ARG:1}、気力-{150*LOCAL:7/ARG:1})
				BASE:ARG:体力 = MAX(BASE:ARG:体力-150*LOCAL:7/ARG:1,500)
				BASE:ARG:気力 = MAX(BASE:ARG:気力-150*LOCAL:7/ARG:1,100)
				;判定に引分
		ENDSELECT
		
		;体力500または気力が100以下で負け
		IF (BASE:ARG:体力 <= 500) || (BASE:ARG:気力 <= 100)
			PRINTFORMW %CALLNAME:ARG%はもう身体が動かせない……
			RETURN 0
		ENDIF

		;戦力0になったら負け
		IF LOCAL:2 < 1
			PRINTFORMW %CALLNAME:ARG%はもう戦うことができないようだ……
			RETURN 0
		ENDIF
		
		;戦力0にしたら勝ち
		IF LOCAL:3 < 1
			PRINTFORMW %NTR_NAME(0)%はもう戦うことができないようだ……
			RETURN 1
		ENDIF
		
		PRINT [0] - まだ戦える [1] - もう降参する
		INPUT
		IF RESULT
			PRINTFORMW %CALLNAME:ARG%は降参した……
			RETURN 0
		ENDIF
		
	WEND
RETURN 0


;---------------------------------------------
;戦闘後処理
;@param ARG   訪問者に対峙したキャラクター番号
;@param ARG:1 戦闘結果(1:勝利,0:敗北)
@COM417_Cleanup(ARG,ARG:1)
	IF ARG:1
		CALL COM417_Win(ARG)
	ELSE
		CALL COM417_Lose(ARG)
	ENDIF
RETURN 0


;---------------------------------------------
;キャラクターの立ち位置(敵味方)を決める関数
;@param ARG  判定するキャラクター番号
;@return 立ち位置(+100〜+1であなたの味方、0で中立、-1〜-100であなたと敵対）
@COM417_Position(ARG)
#FUNCTION
RETURNF LIMIT((100*(CFLAG:ARG:好感度-CFLAG:ARG:屈服度)/MAX(CFLAG:ARG:好感度,1)),-100,100)


;---------------------------------------------
;戦闘勝利時の処理
;@param ARG  訪問者に対峙したキャラクター番号
@COM417_Win(ARG)
		PRINTFORMW %CALLNAME:ARG%は%NTR_NAME(0)%を追い出す事に成功した！
		FLAG:訪問者の現在位置 = 900
		CALL NTR_RESET_VISITOR_ACTION(-1)
		FLAG:訪問者との汚れ判定に使用 = 0
		FLAG:訪問者滞在時間カウンタ = 0
		;屈服度減算
		CFLAG:ARG:屈服度 = MAX(CFLAG:ARG:屈服度 - 50 - RAND:50, 0)
		;全開中の全開で戦った場合、好感度低下。屈服が高いorNTR状態の時は好感度が結構下がる上に時間も消費される。でも、この条件を出せる程まで鍛えた分、屈服低下の補正大。
		IF TFLAG:99 == 2
			FOR LOCAL,0,CHARANUM
				IF LOCAL == ARG
					;自分自身は評価しない
				ELSEIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置
					;好感度と屈服度の差による会話分岐
					IF CFLAG:LOCAL:好感度 > CFLAG:LOCAL:屈服度
						PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:ARG%の猛烈な戦いぶりに
					ELSE
						PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:ARG%の明らかに度を過ぎた戦いぶりに
					ENDIF
					
					;ある程度の屈服度がある状態で恋慕があると自分を守ってくれた事には一応感謝してます
					IF TALENT:LOCAL:3 && ((CFLAG:LOCAL:好感度 < CFLAG:LOCAL:屈服度) || CFLAG:LOCAL:屈服度 > 500)
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 1
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 380
						PRINTFORMW 感謝の言葉を送りつつも、去っていく%NTR_NAME(0)%の背中を見つめていた…
					;屈服度が低い状態で恋慕があると自分を守ってくれた事に感謝してます
					ELSEIF TALENT:LOCAL:3
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 + 25
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 450
						PRINTFORMW その強さで自分の為に戦ってくれた事を感謝した。
					;好感度がある程度あるとちょっとだけ感謝される
					ELSEIF CFLAG:LOCAL:好感度 > CFLAG:LOCAL:屈服度 && CFLAG:LOCAL:好感度 > 500
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 5
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 350
						PRINTFORMW 顔を引きつらせながらも%CALLNAME:ARG%を褒めてくれた。
					;好感度が低いと説教される
					ELSEIF CFLAG:LOCAL:好感度 > CFLAG:LOCAL:屈服度
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 25
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 300
						PRINTFORMW さすがにやり過ぎじゃないかと%CALLNAME:ARG%に説教をした。
					;寝取られた状態では、訪問者を介抱する時間と送り届ける時間とで、結構持っていかれる
					ELSEIF TALENT:6
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 75
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 200
						PRINTFORML 非難の視線を浴びせると、%CALLNAME:ARG%にボロボロにされた%NTR_NAME(0)%を献身的に介抱し、
						PRINTFORMW %NTR_NAME(0)%が動けるようになると、%NTR_NAME(0)%に肩を貸して%CALLNAME:ARG%の方を見ようともせず門まで送り届けた。
						TIME += 20
					;ある一定以上の屈服度があると、訪問者送迎のために時間をとられます
					ELSEIF CFLAG:LOCAL:好感度 < CFLAG:LOCAL:屈服度 && CFLAG:LOCAL:好感度 >499
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 50
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 250
						PRINTFORMW %CALLNAME:ARG%に非難の視線を浴びせつつ、%NTR_NAME(0)%を門まで送り届けた。
						TIME += 10
					;好感度よりも屈服度が高いだけなら睨まれます
					ELSEIF CFLAG:LOCAL:好感度 < CFLAG:LOCAL:屈服度
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 50
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 250
						PRINTFORMW %CALLNAME:ARG%に非難の視線を浴びせた。
					ENDIF
					;屈服度がマイナスになったままだと不具合が起きそうな予感がするので、屈服度がマイナスになった場合は強制的に0にする
					IF CFLAG:LOCAL:屈服度 < 0
						CFLAG:LOCAL:屈服度 = 0
					ENDIF
				ENDIF
			NEXT
		;少しだけ本気出した場合、;勝利時に住人の評価がちょっとダウン(恋慕ならわずかだが好感度が上がる)。屈服が高い時は好感度が下がる上に時間も消費される。
		ELSEIF TFLAG:99 == 1
			FOR LOCAL,0,CHARANUM
				IF LOCAL == ARG
					;自分自身は評価しない
				ELSEIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置
					PRINTFORM %CALLNAME:LOCAL%は
					IF TALENT:3
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 + 1
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 250
						PRINTFORMW とりあえず%CALLNAME:ARG%を褒めてくれた。
					ELSEIF CFLAG:LOCAL:好感度 > CFLAG:LOCAL:屈服度
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 10
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 200
						PRINTFORMW さすがに少しやり過ぎじゃないかと%CALLNAME:ARG%をたしなめた。
					ELSE
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 25
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 150
						PRINTFORMW %CALLNAME:ARG%にボロボロにされた%NTR_NAME(0)%を介抱した。
						TIME += 5
					ENDIF
					;屈服度がマイナスになったままだと不具合が起きそうな予感がするので、屈服度がマイナスになった場合は強制的に0にする
					IF CFLAG:LOCAL:屈服度 < 0
						CFLAG:LOCAL:屈服度 = 0
					ENDIF
				ENDIF
			NEXT
		;本気を出していない場合は変化なし
		ELSE
			;勝利時住人の評価UP、同時にNTR状態の住人の評価ダウン
			FOR LOCAL,0,CHARANUM
				IF LOCAL == ARG
					;自分自身は評価しない
				ELSEIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置
					IF CFLAG:LOCAL:好感度 > CFLAG:LOCAL:屈服度
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 + 10
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 100
						PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:ARG%を褒めてくれた。
					ELSE
						CFLAG:LOCAL:好感度 = CFLAG:LOCAL:好感度 - 10
						CFLAG:LOCAL:屈服度 = CFLAG:LOCAL:屈服度 - 100
						PRINTFORMW %CALLNAME:LOCAL%は去って行く%NTR_NAME(0)%の背中を見つめていた。
					ENDIF
					;屈服度がマイナスになったままだと不具合が起きそうな予感がするので、屈服度がマイナスになった場合は強制的に0にする
					IF CFLAG:LOCAL:屈服度 < 0
						CFLAG:LOCAL:屈服度 = 0
					ENDIF
				ENDIF
			NEXT
		ENDIF
RETURN 0


;---------------------------------------------
;戦闘敗北時の処理
;@param ARG  訪問者に対峙したキャラクター番号
@COM417_Lose(ARG)

	VARSET LOCAL

	PRINTFORMW %CALLNAME:ARG%は%NTR_NAME(0)%に敗けてしまった……
	;貞操帯の鍵没収
	IF FLAG:貞操帯鍵購入フラグ
		PRINTFORMW %CALLNAME:(FLAG:貞操帯鍵購入フラグ)%の貞操帯の鍵を没収されてしまった……
		FLAG:貞操帯鍵購入フラグ = 0
		FLAG:貞操帯鍵有効カウンタ = 0
	ENDIF
	FOR LOCAL,0,CHARANUM
		IF LOCAL == ARG
			;自分自身はキツいお仕置きが待っている
		ELSEIF CFLAG:LOCAL:現在位置 == 21 && CFLAG:ARG:現在位置 != 21
			;野外トイレが危険な感じになってきたので脱出
			IF CFLAG:ARG:現在位置 == 20
				;庭に居たら正門まで逃げる(守衛小屋は鍵の可能性があるので避ける)
				CFLAG:LOCAL:現在位置 = 1
			ELSE
				;それ以外の場合は庭へ逃げる
				CFLAG:LOCAL:現在位置 = 20
			ENDIF
		ELSEIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置
			;あなたに味方した娘にはお仕置きだよねー
			SELECTCASE COM417_Position(LOCAL)
				CASE 0 TO 19
					;さりげなく援護していた場合、軽いお仕置き
					CALL COM417_Lose_Punishment_Easy(LOCAL)
					PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:ARG%に失望したようだ。
					CFLAG:LOCAL:好感度 -= MAX(CFLAG:LOCAL:好感度 / 50,100)
				CASE 20 TO 49
					;積極的に援護していた場合、普通のお仕置き
					CALL COM417_Lose_Punishment_Normal(LOCAL)
					LOCAL:1 += RESULT
					PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:ARG%に大きく失望したようだ。
					CFLAG:LOCAL:好感度 -= MAX(CFLAG:LOCAL:好感度 / 25,250)
				CASE IS >= 50
					;全力で援護していた場合、キツいお仕置き
					CALL COM417_Lose_Punishment_Extra(LOCAL)
					LOCAL:1 += RESULT
					PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:ARG%にひどく失望したようだ。
					CFLAG:LOCAL:好感度 -= MAX(CFLAG:LOCAL:好感度 / 10,500)
				CASEELSE
					;それ以外は大丈夫
					PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:ARG%に失望したようだ。
					CFLAG:LOCAL:好感度 -= MAX(CFLAG:LOCAL:好感度 / 50,100)
			ENDSELECT
		ENDIF
	NEXT
	;他に誰か連れて行かれてたら助けてもらえない
	CALL COM417_Lose_Punishment_Hard(ARG,LOCAL:1)
	CALL COM417_Lose_Punishment_Extra_Continue(ARG)
RETURN 0


;---------------------------------------------
;戦闘敗北時の(優しい)お仕置き処理(ぱんつ回収＆ローター挿入)
;@param ARG  訪問者に対峙したキャラクター番号
@COM417_Lose_Punishment_Easy(ARG)

	VARSET LOCAL

	PRINTFORML %NTR_NAME(0)%は%CALLNAME:ARG%に穿いている下着を脱いで、秘部を晒すように命じた
	IF EQUIP:ARG:下半身下着１ == 0 && EQUIP:ARG:下半身下着２ == 0
		;ぱんつはいてない
		PRINTFORML %CALLNAME:ARG%はしばらく黙っていたが、やがて小さな声で下着は穿いていないと返事をした
		PRINTFORML %NTR_NAME(0)%は意地悪な笑みを浮かべ、見せてみろ、と言った……
		SOURCE:ARG:露出 += 200
		SOURCE:ARG:欲情 += 100 + 200 * ABL:ARG:欲望
		SOURCE:ARG:受動 += 100 + 200 * ABL:ARG:従順
		CFLAG:ARG:屈服度 += 1
	ELSEIF EQUIP:下半身下着１ == 100
		;貞操帯
		PRINTFORML %CALLNAME:ARG%はしばらく黙っていたが、鍵が無ければ脱ぎたくても脱げないのだと返事をした
		SOURCE:ARG:欲情 += 100 + 100 * ABL:ARG:欲望
		SOURCE:ARG:受動 += 100 + 100 * ABL:ARG:従順
		CFLAG:ARG:屈服度 += 2
		RETURN 0
	ELSE	;ぱんつはかせたくない
		PRINTFORM %CALLNAME:ARG%はしばらく黙っていたが、やがて
		IF EQUIP:ARG:下半身下着１
			PRINTFORM %PANTS_DESCRIPTION(EQUIP:ARG:下半身下着１)%%PANTSNAME(EQUIP:ARG:下半身下着１)%
		ELSEIF EQUIP:ARG:下半身下着２
			PRINTFORM %PANTS_DESCRIPTION(EQUIP:ARG:下半身下着２)%%PANTSNAME(EQUIP:ARG:下半身下着２)%
		ELSE
			;発生しないはずだけど一応ね
			PRINTFORM 下着
		ENDIF
		PRINTFORML を脱ぐと、%NTR_NAME(0)%に手渡した……
		CFLAG:ARG:服装_下半身下着２ = 0
		SOURCE:ARG:露出 += 200
		SOURCE:ARG:欲情 += 100 + 200 * ABL:ARG:欲望
		SOURCE:ARG:受動 += 100 + 200 * ABL:ARG:従順
		CFLAG:ARG:屈服度 += 1
	ENDIF

	IF HAS_VAGINA(ARG)
		IF TALENT:ARG:処女
			;見るまでわからないからね、仕方がないね
			PRINTFORML %NTR_NAME(0)%は%CALLNAME:ARG%の秘唇を押し広げ、処女膜を確認すると、次は処女を奪ってやる、と脅した
			SOURCE:ARG:露出 += 100
			SOURCE:ARG:恐怖 += 500
			CFLAG:ARG:屈服度 += 10
		ELSE
			IF CFLAG:ARG:ローター挿入 != 0
				;ローターは新しいのに交換しようねー
				PRINTFORML %NTR_NAME(0)%は%CALLNAME:ARG%の秘所に押し込まれているローターの電池を取り替えた
			ELSE
				PRINTFORML %NTR_NAME(0)%は%CALLNAME:ARG%の秘所にローターを挿入して固定した
			ENDIF
			CFLAG:ARG:ローター挿入 = 240
			SOURCE:ARG:露出 += 200
			SOURCE:ARG:欲情 += 100 + 200 * ABL:ARG:欲望
			SOURCE:ARG:受動 += 100 + 200 * ABL:ARG:従順
			CFLAG:ARG:屈服度 += 3
		ENDIF
		PRINT そのまま
	ENDIF
	IF CFLAG:ARG:ローターA挿入 != 0
		;ローターは新しいのに交換しようねー
		PRINTFORML %NTR_NAME(0)%は%CALLNAME:ARG%のアヌスに押し込まれているローターの電池を取り替えた
	ELSE
		PRINTFORML %NTR_NAME(0)%は%CALLNAME:ARG%のアヌスにローターを挿入して固定した
	ENDIF
	CFLAG:ARG:ローターA挿入 = 240
	SOURCE:ARG:露出 += 200
	SOURCE:ARG:欲情 += 100 + 200 * ABL:ARG:欲望
	SOURCE:ARG:受動 += 100 + 200 * ABL:ARG:従順
	CFLAG:ARG:屈服度 += 3

RETURN 0
;---------------------------------------------
;戦闘敗北時の(普通の)お仕置き処理(野外トイレ行き)
;@param ARG   訪問者に対峙したキャラクター番号
;@param ARG:1 他人が口をだせるかどうか(0:出せる,1:出せない)
;@return 結果(0:助かった,1:野外トイレ行き)
@COM417_Lose_Punishment_Normal(ARG,ARG:1)
	
	VARSET LOCAL

	PRINTFORML %NTR_NAME(0)%は%CALLNAME:ARG%に、たっぷりと犯してやるから野外トイレで待て、と命じた。

	IF TALENT:ARG:処女
		PRINTFORML %CALLNAME:ARG%は恐怖で震えている……
		SOURCE:ARG:恐怖 += 500
		CFLAG:ARG:屈服度 += 10
	ELSE
		PRINTFORML %CALLNAME:ARG%は屈辱に唇を噛みしめた……
		SOURCE:ARG:欲情 += 100 + 200 * ABL:ARG:欲望
		SOURCE:ARG:受動 += 100 + 200 * ABL:ARG:従順
	ENDIF
	
	IF ARG:1 == 0
		;あなた対応ができなくて詰んだ……仕方ないんであなた抜きループ
		;流石にやり過ぎ、と言って貰えるかどうかの判定
		FOR LOCAL,1,CHARANUM
			IF LOCAL == ARG
				;自分自身は助けられませんね？
			ELSEIF TALENT:LOCAL:NTR
				;NTRだと助けてくれません
			ELSEIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置
				;同じ位置にいるならワンチャンある
				IF COM417_Position(LOCAL) <20
					;あなた側にさりげなく援護、程度なら口出しする権利がある
					;相性補正とか入れてみたいけど、面倒なのでとりあえず単純に。
					IF LOCAL:1 == 0
						PRINTFORM %CALLNAME:LOCAL%
					ELSE
						PRINTFORM と%CALLNAME:LOCAL%
					ENDIF
					LOCAL:1 ++
				ENDIF
			ENDIF
		NEXT
	ENDIF
	IF LOCAL:1 > 0
		;助けて貰えた
		PRINTFORMW が非難すると、%NTR_NAME(0)%は渋々とあきらめたようだ……
		CALL COM417_Lose_Punishment_Easy(ARG)
		RETURN 0
	ELSE
		;助けて貰えなかった
		PRINTFORMW %CALLNAME:ARG%はうつむいて、犯されるためだけに野外トイレへと歩いて行った……
		CFLAG:ARG:現在位置 = 19
		RETURN 1
	ENDIF
RETURN 0


;---------------------------------------------
;戦闘敗北時の(厳しい)お仕置き処理(お持ち帰り)
;@param ARG  訪問者に対峙したキャラクター番号
@COM417_Lose_Punishment_Extra(ARG)

	VARSET LOCAL
	
	IF !GETBIT(FLAG:ＮＴＲパッチ設定,6)
		;お持ち帰り禁止なら野外トイレ行き（強制）
		CALL COM417_Lose_Punishment_Normal(ARG,1)
		RETURN 0
	ENDIF

	PRINTFORML %NTR_NAME(0)%は%CALLNAME:ARG%に、正門で待っているように命じた
	PRINTFORML %CALLNAME:ARG%が恐る恐る理由を聞くと、%NTR_NAME(0)%は自宅に連れ帰って可愛がってやるからだ、と答えた

	IF TALENT:ARG:処女
		PRINTFORML %CALLNAME:ARG%は恐怖のあまり失神しそうになっている……
		SOURCE:ARG:恐怖 += 1000
		CFLAG:ARG:屈服度 += 100
	ELSE
		PRINTFORML %CALLNAME:ARG%は周りを見回し、助けを求めた……
		SOURCE:ARG:欲情 += 100 + 200 * ABL:ARG:欲望
		SOURCE:ARG:受動 += 100 + 200 * ABL:ARG:従順
	ENDIF
	
	;あなた対応ができなくて詰んだ……仕方ないんであなた抜きループ
	;流石にやり過ぎ、と言って貰えるかどうか……？
	FOR LOCAL,1,CHARANUM
		IF LOCAL == ARG
			;自分自身は助けられませんね？
		ELSEIF TALENT:LOCAL:NTR
			;NTRだと助けてくれません
		ELSEIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置
			;同じ位置にいるならワンチャンある
			IF COM417_Position(LOCAL) <20
				;あなた側にさりげなく援護、程度なら口出しする権利がある
				;相性補正とか入れてみたいけど、面倒なのでとりあえず単純に。
				IF LOCAL:1 == 0
					PRINTFORM %CALLNAME:LOCAL%
				ELSE
					PRINTFORM と%CALLNAME:LOCAL%
				ENDIF
				LOCAL:1 ++
			ENDIF
		ENDIF
	NEXT
	IF LOCAL:1 > 0
		;助けて貰えたが、野外トイレへご招待
		PRINTFORMW が非難すると、%NTR_NAME(0)%は連れ帰るのをあきらめたようだ……
		CALL COM417_Lose_Punishment_Normal(ARG,1)
		RETURN 1
	ELSE

		IF TALENT:ARG:処女
			;処女守りたい？
			CALL COM417_Save_the_Virgin(ARG,1)
			IF RESULT == 1
				RETURN 0
			ENDIF
		ENDIF

		;助けて貰えなかった
		PRINTFORMW %CALLNAME:ARG%は震えて泣きながら、正門の前で%NTR_NAME(0)%を待っている……
		;現在位置を強制的に開錠
		FLAG:(300 + FLAG:訪問者の現在位置) = 0
		;正門と言いつつそのまま訪問者宅へ移動。本当に正門へ移動させると誤爆拉致があるので
		CFLAG:ARG:現在位置 = 900
		;訪問者はまだやることがあるので後で移動します
		;FLAG:訪問者の現在位置 = 900
		CALL NTR_RESET_VISITOR_ACTION(ARG)
		FLAG:訪問者との汚れ判定に使用 = 0
		FLAG:訪問者滞在時間カウンタ = 0
		CFLAG:ARG:NTRカウンターセクハラ = 0
	ENDIF
	
RETURN 0

;---------------------------------------------
;戦闘敗北時の(厳しい)お仕置き処理(野外トイレメイン処理)
;@param ARG  訪問者に対峙したキャラクター番号
;@param ARG:1 他人が口をだせるかどうか(0:出せる,1以上:出せない)
@COM417_Lose_Punishment_Hard(ARG,ARG:1)

	VARSET LOCAL
	
	IF ARG:1 == 0
		FOR LOCAL,1,CHARANUM
			IF LOCAL == ARG
				;自分自身は助けられませんね？
			ELSEIF TALENT:LOCAL:NTR
				;NTRだと助けてくれません
			ELSEIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置
				;同じ位置にいるならワンチャンある
				IF COM417_Position(LOCAL) <20
					;あなた側にさりげなく援護、程度なら口出しする権利がある
					;相性補正とか入れてみたいけど、面倒なのでとりあえず単純に。
					IF LOCAL:1 == 0
						PRINTFORM %CALLNAME:MASTER%は野外トイレへ連れて行かれそうになったが、%CALLNAME:LOCAL%
					ELSE
						PRINTFORM と%CALLNAME:LOCAL%
					ENDIF
					LOCAL:1 ++
				ENDIF
			ENDIF
		NEXT
	ENDIF
	IF LOCAL:1 > 0
		;助けてもらえた
		PRINTW によって阻止された
		RETURN 0
	ELSE
		CFLAG:ARG:現在位置 = 21
		FLAG:訪問者の現在位置 = 21
		FLAG:訪問者滞在時間カウンタ = 0
		PRINTFORMW %CALLNAME:ARG%は%NTR_NAME(0)%によって公衆トイレに連れ込まれた
		PRINTFORMW 　　　　　　・
		PRINTFORMW 　　　　　　・
		PRINTFORMW 　　　　　　・
		FOR LOCAL,0,CHARANUM
			IF CFLAG:LOCAL:現在位置 == 21
				IF TALENT:LOCAL:処女
					;処女守りたい？
					CALL COM417_Save_the_Virgin(LOCAL)
				ELSE
					RESULT =0
				ENDIF
				IF RESULT == 0
					CALL COM417_Fucking(LOCAL,RESULT)
				ENDIF
			ENDIF
		NEXT
	ENDIF
RETURN 0


;---------------------------------------------
;気が済むまで犯す処理
;@param ARG 犠牲者のキャラクター番号
;@param ARG:1 後ろだけ使うなら1
@COM417_Fucking(ARG,ARG:1)

	VARSET LOCAL
	
	;LOCAL:1 経過時間(分)
	LOCAL:1 = 0
	;LOCAL:2 連続レイプ確率
	LOCAL:2 = 5
	;LOCAL:3 Ｖレイプ回数
	LOCAL:3 = 0
	;LOCAL:4 Ａレイプ回数
	LOCAL:4 = 0

	;飽きるまで犯す
	WHILE 1
		;時間経過
		LOCAL:1 += RAND:5 + 18
		;犯されるごとに屈服度上昇
		CFLAG:ARG:屈服度 += (5 + RAND:5)
		;基本前重視の判定
		IF RAND:3 && HAS_VAGINA(ARG)
			;Ｖレイプ回数増加
			LOCAL:3 ++
			CALL COM417_Ntr_Rape_V(ARG)
		ELSE
			;Aレイプ回数増加
			LOCAL:4 ++
			CALL COM417_Ntr_Rape_A(ARG)
		ENDIF

		;犯し飽きたかどうかの判定
		SIF RAND:(LOCAL:2) == 0
			BREAK
		LOCAL:2 = MAX(LOCAL:2--,2)

	WEND

	IF LOCAL:3 > 0 && LOCAL:4 == 0
		PRINTFORMW %CALLNAME:ARG%は{LOCAL:3}回、{LOCAL:1/60}時間{LOCAL:1%60}分もの間性器を犯された…
	ELSEIF LOCAL:3 == 0 && LOCAL:4 > 0
		PRINTFORMW %CALLNAME:ARG%は{LOCAL:4}回、{LOCAL:1/60}時間{LOCAL:1%60}分もの間アナルを犯された…
	ELSEIF LOCAL:3 > 0 && LOCAL:4 > 0
		PRINTFORMW %CALLNAME:ARG%は性器を{LOCAL:3}回、アナルを{LOCAL:4}回、{LOCAL:1/60}時間{LOCAL:1%60}分もの間犯された…
	ENDIF

	;MASTERのレイプ回数
	IF ARG == MASTER
		FLAG:MASTERが犯された回数 += LOCAL:3 + LOCAL:4
	ENDIF
	;時間経過
	TIME += LOCAL:1
RETURN 0


;---------------------------------------------
;犯す処理Ｖ
;@param ARG 犠牲者のキャラクター番号
@COM417_Ntr_Rape_V(ARG)

	VARSET LOCAL
	
	;経験増加
	EXP:ARG:Ｖ経験 ++
	EXP:ARG:Ｖ性交経験 ++

	;膣内射精される
	TRYCALL 精力取得, 998
	TRYCALL NTR_SAMEN_EXP_PLUS(ARG, 0)
	TRYCALL 精子取得, 998, ARG

	;簡易絶頂判定
	IF ABL:ARG:Ｖ感覚 > RAND:5
		EXP:ARG:Ｖ絶頂経験 ++
		EXP:ARG:絶頂経験 ++
		;絶頂したらご褒美にもう一回精子取得
		TRYCALL 精子取得, 998, ARG
	ENDIF

	;処女判定
	IF TALENT:ARG:処女
		PRINTFORML %CALLNAME:ARG%の処女は%NTR_NAME(0)%によって散らされてしまった・・
		PRINTFORML ＜%CALLNAME:ARG%は処女を失いました＞
		TRYCALL COM417_Lost_Virgin(ARG,-1)
		TALENT:ARG:処女 = 0
		;破瓜血付与(この後の汚れ判定で)
		SETBIT FLAG:訪問者との汚れ判定に使用,6
		CFLAG:ARG:屈服度 += (10 + RAND:10)
	ENDIF

	;汚れ判定
	;訪問者の精液汚れセット
	SETBIT FLAG:訪問者との汚れ判定に使用,2
	FOR LOCAL:0,0,6
		IF GETBIT(STAIN:ARG:Ｖ,LOCAL:0)
			SETBIT FLAG:訪問者との汚れ判定に使用,LOCAL:0
		ENDIF
		IF GETBIT(STAIN:ARG:膣内,LOCAL:0)
			SETBIT FLAG:訪問者との汚れ判定に使用,LOCAL:0
		ENDIF
		IF GETBIT(FLAG:訪問者との汚れ判定に使用,LOCAL:0)
			SETBIT STAIN:ARG:Ｖ,LOCAL:0
			SETBIT STAIN:ARG:膣内,LOCAL:0
		ENDIF
	NEXT

RETURN 0


;---------------------------------------------
;犯す処理Ａ
;@param ARG 犠牲者のキャラクター番号
@COM417_Ntr_Rape_A(ARG)

	VARSET LOCAL
	
	;経験増加
	EXP:ARG:Ａ経験 ++
	EXP:ARG:Ａ性交経験 ++
	EXP:ARG:精液経験 ++

	;簡易絶頂判定
	IF ABL:ARG:Ａ感覚 > RAND:5
		EXP:ARG:Ａ絶頂経験 ++
		EXP:ARG:絶頂経験 ++
		IF HAS_PENIS(ARG)
			;絶頂したらトコロテン
			SETBIT STAIN:ARG:Ｐ,2
			EXP:ARG:射精経験 ++
		ENDIF
	ENDIF

	;汚れ判定
	;訪問者の精液汚れセット
	SETBIT FLAG:訪問者との汚れ判定に使用,2
	FOR LOCAL:0,0,6
		IF GETBIT(STAIN:ARG:Ａ,LOCAL:0)
			SETBIT FLAG:訪問者との汚れ判定に使用,LOCAL:0
		ENDIF
		IF GETBIT(STAIN:ARG:腸内,LOCAL:0)
			SETBIT FLAG:訪問者との汚れ判定に使用,LOCAL:0
		ENDIF
		IF GETBIT(FLAG:訪問者との汚れ判定に使用,LOCAL:0)
			SETBIT STAIN:ARG:Ａ,LOCAL:0
			SETBIT STAIN:ARG:腸内,LOCAL:0
		ENDIF
	NEXT

RETURN 0


;---------------------------------------------
;戦闘敗北時の(厳しい)お仕置き処理の続き(お持ち帰り)
@COM417_Lose_Punishment_Extra_Continue(ARG)
	
	VARSET LOCAL
	
	LOCAL:1 = 0
	LOCAL:2 = 0
	FOR LOCAL,1,CHARANUM
		IF CFLAG:LOCAL:現在位置 == 900
			LOCAL:2 = LOCAL
			IF LOCAL:1 == 0
				PRINTFORM 正門では%CALLNAME:LOCAL%
			ELSE
				PRINTFORM と%CALLNAME:LOCAL%
			ENDIF
			LOCAL:1++
		ENDIF
	NEXT
	IF LOCAL:1==0
		;レイプ後にみんな野外トイレに居て会話始まっちゃうのはアレなので訪問者を庭へ逃がす
		FLAG:訪問者の現在位置 = 16
		RETURN 0
	ELSE
		PRINTFORM が%NTR_NAME(0)%を待っていた……
		;ここで訪問者も帰る
		FLAG:訪問者の現在位置 = 900
		IF LOCAL:1 > 1
			PRINTFORMW %NTR_NAME(0)%は{LOCAL:1}人を連れると、どこかへ去って行った……
		ELSE
			PRINTFORMW %NTR_NAME(0)%は%CALLNAME:(LOCAL:2)%を連れると、どこかへ去って行った……
		ENDIF
		
	ENDIF

RETURN 0

;---------------------------------------------
;処女を奪うのだけは許してください！なんでもしますから！
;@param ARG   犠牲者のキャラクター番号
;@param ARG:1 野外トイレなら0、持ち帰りなら1
;@return 0:奪ってもいいです,1:奪わないでください
@COM417_Save_the_Virgin(ARG,ARG:1)
	;基本あなた限定なので、意図的にMASTERを使っています。
	;ちょっと関数長すぎる上に汚いんでリファクタリングしたいですね
	VARSET LOCAL
	
	IF !TALENT:ARG:処女
		;非処女は助ける必要は無いね
		RETURN 0
	ENDIF

	IF ARG == MASTER
		;自分は助かりません
		RETURN 0
	ENDIF
	
	PRINTFORML このままでは%CALLNAME:ARG%の処女が奪われてしまう……！
	
	PRINT [0] - それだけはやめてくれと頼む [1] - 泣く泣くあきらめる
	INPUT
	IF RESULT
		PRINTFORMW %CALLNAME:MASTER%は、何も言うことができなかった……
		RETURN 0
	ENDIF
	
	PRINTFORML %CALLNAME:MASTER%が処女を奪うのだけは許してくれ、と懇願すると
	PRINTFORMW %NTR_NAME(0)%は、何かとても酷いことを思いついたようだ……
	PRINTL

	PRINTFORML %NTR_NAME(0)%は、しばらくの間%CALLNAME:ARG%の身体を弄り回すと、ポケットから小さな印章を取りだし
	;LOCAL:1 今回押させる数
	;LOCAL:2 今回押させる場所
	PRINTFORM %CALLNAME:ARG%の
	FOR LOCAL:0,5,0,-1
		IF (LOCAL:1<=ARG:1) && (GETBIT(LOCAL:2,4)==0) && (GETBIT(CFLAG:ARG:娼婦紋,4)==0) && (ABL:ARG:親密 > LOCAL:0)
			;右頬優先。目に見えるから。あとちゅっちゅ防止
			PRINTFORM %"と"*(LOCAL:1>0)+"右頬"%
			SETBIT LOCAL:2,4
			LOCAL:1++
		ENDIF
		IF (LOCAL:1<=ARG:1) && (GETBIT(LOCAL:2,0)==0) && (GETBIT(CFLAG:ARG:娼婦紋,0)==0) && (ABL:ARG:Ｃ感覚 > LOCAL:0)
			PRINTFORM %"と"*(LOCAL:1>0)+"恥丘"%
			SETBIT LOCAL:2,0
			LOCAL:1++
		ENDIF
		IF (LOCAL:1<=ARG:1) && (GETBIT(LOCAL:2,1)==0) && (GETBIT(CFLAG:ARG:娼婦紋,1)==0) && (ABL:ARG:Ａ感覚 > LOCAL:0)
			PRINTFORM %"と"*(LOCAL:1>0)+"左尻"%
			SETBIT LOCAL:2,1
			LOCAL:1++
		ENDIF
		IF (LOCAL:1<=ARG:1) && (GETBIT(LOCAL:2,2)==0) && (GETBIT(CFLAG:ARG:娼婦紋,2)==0) && (ABL:ARG:Ｂ感覚 > LOCAL:0)
			PRINTFORM %"と"*(LOCAL:1>0)+"胸元"%
			SETBIT LOCAL:2,2
			LOCAL:1++
		ENDIF
		IF (LOCAL:1<=ARG:1) && (GETBIT(LOCAL:2,3)==0) && (GETBIT(CFLAG:ARG:娼婦紋,3)==0) && (ABL:ARG:Ｖ感覚 > LOCAL:0)
			;挿入は優先度が低い。訪問者は優しいね。
			PRINTFORM %"と"*(LOCAL:1>0)+"太腿"%
			SETBIT LOCAL:2,3
			LOCAL:1++
		ENDIF
		IF LOCAL:1 > ARG:1
			BREAK
		ENDIF
	NEXT

	PRINTFORMW に印章を押しつければ処女は奪わずにおいてやる、と言った
	PRINTL
	
	IF ARG == 3
		;ぱっちぇさん見ただけでわかるねすごいね。だが絶望しかない
		PRINTFORMW %CALLNAME:ARG%は印章を見て青ざめたが、%NTR_NAME(0)%が軽く脅しをかけると、口を噤んで%CALLNAME:MASTER%の方をじっと見つめた
	ENDIF
	
	IF EXP:MASTER:買春経験
		PRINTFORMW 印章に刻まれているのは、%NTR_NAME(0)%から買う「便所」の鍵と同じ紋だ……。
	ENDIF
	
	IF TALENT:ARG:処女 == 1
		;純正処女だと……まあ、うん。なんとかなるはなるよね
		PRINTFORMW 凄く嫌な予感がする。これを押したとして、本当に%CALLNAME:ARG%を助けられるのだろうか……？
	ELSE
		;再生処女だと取り返しがつかない事になります
		PRINTFORMW 凄く嫌な予感がする。これを押してしまったら、もう取り返しがつかないような……。
	ENDIF

	PRINT [0] - 処女を守るために押しつける [1] - 無理だ。これは押せない……
	INPUT
	IF RESULT
		PRINTFORMW %NTR_NAME(0)%は、どうやら処女はいらないらしいな、と%CALLNAME:ARG%に告げた……
		RETURN 0
	ENDIF
	
	LOCAL:1 = 0
	PRINTFORM %CALLNAME:MASTER%は印章を受け取ると、%CALLNAME:ARG%の
	IF GETBIT(LOCAL:2,0)
		PRINTFORM %"と"*(LOCAL:1>0)+"恥丘"%
		LOCAL:1++
	ENDIF
	IF GETBIT(LOCAL:2,1)
		PRINTFORM %"と"*(LOCAL:1>0)+"左尻"%
		LOCAL:1++
	ENDIF
	IF GETBIT(LOCAL:2,2)
		PRINTFORM %"と"*(LOCAL:1>0)+"胸元"%
		LOCAL:1++
	ENDIF
	IF GETBIT(LOCAL:2,3)
		PRINTFORM %"と"*(LOCAL:1>0)+"太腿"%
		LOCAL:1++
	ENDIF
	IF GETBIT(LOCAL:2,4)
		PRINTFORM %"と"*(LOCAL:1>0)+"右頬"%
		LOCAL:1++
	ENDIF

	PRINTFORMW に押しつけた。
	PRINTFORMW 印章を離すと、押したところには淫靡さを感じさせる、赤紫色の紋が残されている。
	PRINTL
	PRINTFORML %CALLNAME:MASTER%が振り返ると、%NTR_NAME(0)%は悪魔のような笑みを浮かべ、
	PRINTFORMW それは生娘を買い取る時に押させる紋で、押した%CALLNAME:MASTER%は%CALLNAME:ARG%に手を出せなくなるのだ、と種明かしをした。
	
	IF GETBIT(LOCAL:2,0)
		SETBIT CFLAG:ARG:娼婦紋,0
		PRINTFORML %CALLNAME:ARG%は恥丘に娼婦紋を得た
	ENDIF
	IF GETBIT(LOCAL:2,1)
		SETBIT CFLAG:ARG:娼婦紋,1
		PRINTFORML %CALLNAME:ARG%は左尻に娼婦紋を得た
	ENDIF
	IF GETBIT(LOCAL:2,2)
		SETBIT CFLAG:ARG:娼婦紋,2
		PRINTFORML %CALLNAME:ARG%は胸元に娼婦紋を得た
	ENDIF
	IF GETBIT(LOCAL:2,3)
		SETBIT CFLAG:ARG:娼婦紋,3
		PRINTFORML %CALLNAME:ARG%は太腿に娼婦紋を得た
	ENDIF
	IF GETBIT(LOCAL:2,4)
		SETBIT CFLAG:ARG:娼婦紋,4
		PRINTFORML %CALLNAME:ARG%は右頬に娼婦紋を得た
	ENDIF
	
	PRINTFORMW %NTR_NAME(0)%は他の誰かに捧げるその日まで、%CALLNAME:ARG%の処女を大事にしてやれ、と言い笑った。
	RETURN 1
	
RETURN 0


;---------------------------------------------
;処女が失われた時の処理
;@param ARG   奪われたキャラ
;@param ARG:1 奪ったキャラ
@COM417_Lost_Virgin(ARG,ARG:1)

	IF CFLAG:ARG:娼婦紋 == 0
		RETURN 0
	ENDIF

	IF ARG:1 == MASTER
		PRINTFORML %CALLNAME:ARG%の娼婦紋はもう二度と消えなくなってしまった……
		RETURN 0
	ENDIF

	IF TALENT:ARG:処女 == 2
		PRINTFORML 処女を奪われたにもかかわらず、%CALLNAME:ARG%の娼婦紋は消えなかった……
		RETURN 0
	ENDIF

	PRINTFORML %CALLNAME:MASTER%以外に処女を奪われ、%CALLNAME:ARG%の娼婦紋は跡形も無く消え去った……。
	CFLAG:ARG:娼婦紋 = 0

RETURN 0


;---------------------------------------------
;紋による接触可否チェック
;@param ARG:0 調教者
;@param ARG:1 対象者
;@param ARG:2 部位(0:Ｃ,1:Ｖ,2:Ａ,3:Ｂ,4:口)
;@return 0:触れる 1:触れない
@COM417_is_Protected(ARG:0,ARG:1,ARG:2)
	
	VARSET LOCAL

	SELECTCASE ARG:2
		CASE 0
			LOCALS:0 = クリトリス
			LOCALS:1 = 恥丘
		CASE 1
			LOCALS:0 = 秘唇
			LOCALS:1 = 太腿
		CASE 2
			LOCALS:0 = 尻
			LOCALS:1 = 左尻
		CASE 3
			LOCALS:0 = 胸
			LOCALS:1 = 胸元
		CASE 4
			LOCALS:0 = 口
			LOCALS:1 = 右頬
		CASEELSE
			RETURN 0
	ENDSELECT
	
	
	IF GETBIT(CFLAG:(ARG:1):娼婦紋,ARG:2)

		;充填率
		CALL COM417_GetFillingRateByPublicSperm(ARG:1)
		SELECTCASE (RESULT:1 * RESULT:0)
			CASE IS >= 200
				;満タンならコマンドが実行できる
				PRINTFORML %CALLNAME:(ARG:1)%の%LOCALS:1%に押された紋が、鮮やかなピンク色に変わっている……
				PRINTFORML 精液で膨れあがった%CALLNAME:(ARG:1)%の下腹を撫で、%CALLNAME:(ARG:0)%はその肢体にむしゃぶりついた
				PRINTFORML これだけ精液が注ぎ込まれたのなら、数日は娼婦紋が効果を失ったままだろう……
				RETURN 0
			CASE 100 TO 199
				;満タンならコマンドが実行できる
				PRINTFORML %CALLNAME:(ARG:1)%の%LOCALS:1%に押された紋が、ピンク色に変わっている……
				PRINTFORML どうやら、%CALLNAME:(ARG:0)%以外の精液が子宮を満たしている間だけは、娼婦紋が効果を失うようだ
				PRINTFORML 久しぶりに%CALLNAME:(ARG:1)%に触れた%CALLNAME:(ARG:0)%は、その感触を十分に堪能した
				RETURN 0
			CASE 50 TO 99
				PRINTFORML %CALLNAME:(ARG:0)%は%CALLNAME:(ARG:1)%の%LOCALS%に触れようとしたが、見えない壁のようなものに阻まれた
				PRINTFORML %CALLNAME:(ARG:1)%の%LOCALS:1%に押された紋が、赤色に変わっている……
			CASE 1 TO 49
				PRINTFORML %CALLNAME:(ARG:0)%は%CALLNAME:(ARG:1)%の%LOCALS%に触れようとしたが、見えない壁のようなものに阻まれた
				PRINTFORML %CALLNAME:(ARG:1)%の%LOCALS:1%に押された紋が、赤紫色に変わっている……
			CASEELSE
				IF RESULT:1 == 0 && RESULT:0 > 99
					PRINTFORML %CALLNAME:(ARG:1)%の子宮は精液で膨れあがっているのに、%LOCALS:1%に押された紋は紫色のままだ。
					PRINTFORML %CALLNAME:(ARG:0)%の精液が注がれた証拠ではあるが、もう%CALLNAME:(ARG:1)%に触れることはできない……
				ELSE
					PRINTFORML %CALLNAME:(ARG:0)%は%CALLNAME:(ARG:1)%の%LOCALS%に触れようとしたが、見えない壁のようなものに阻まれた
					PRINTFORML %CALLNAME:(ARG:1)%の%LOCALS:1%には、紫色の紋が押されている……
					PRINTFORML しばらくの間いろいろと試してみたが、紋が押されている限り指一本触れることはできないらしい。
				ENDIF
		ENDSELECT
		
		
		TIME += 10

		IF CFLAG:(ARG:1):好感度 > 0
			PRINTFORML %CALLNAME:(ARG:1)%は触れて貰えない事に不満を感じているようだ……
			;欲情していると反感が伸びる
			PALAM:(ARG:1):反感 += MIN( PALAM:(ARG:1):欲情/MAX(CFLAG:(ARG:1):好感度,1),300)
			;欲情していると好感度も下がる
			CFLAG:(ARG:1):好感度 = MAX( CFLAG:(ARG:1):好感度 - MIN(PALAM:(ARG:1):欲情 ,30),0)
		ELSE
			PRINTFORML %CALLNAME:(ARG:1)%はつまらなさそうに%CALLNAME:(ARG:0)%を見た……
		ENDIF

		RETURN 1
	ENDIF
RETURN 0


;---------------------------------------------
;紋による接触可否チェック(チェックのみ)
;@param ARG:0 調教者
;@param ARG:1 対象者
;@param ARG:2 部位(0:Ｃ,1:Ｖ,2:Ａ,3:Ｂ,4:口)
;@return 0:触れる 1:触れない
@COM417_is_Protected_CheckOnly(ARG:0,ARG:1,ARG:2)
#FUNCTION
	VARSET LOCAL
	IF !GETBIT(CFLAG:(ARG:1):娼婦紋,ARG:2)
		RETURNF 0
	ENDIF
	FOR LOCAL:0, 0, 301
		SIF LOCAL:0 == CHARANUM
			LOCAL:0 = 299
		DA:(ARG:1):(LOCAL:0) = 0
		REPEAT 5
			DA:(ARG:1):(LOCAL:0) += TA:(ARG:1):(LOCAL:0):COUNT
		REND
		IF DA:(ARG:1):(LOCAL:0) > 0
			IF LOCAL:0 == MASTER
				RETURNF 1
			ENDIF
			LOCAL:2 += DA:(ARG:1):(LOCAL:0)
		ENDIF
	NEXT

	SELECTCASE LIMIT(100*(LOCAL:2)/(CFLAG:(ARG:1):子宮内体積),0,200)
		CASE IS >= 100
			RETURNF 0
		CASEELSE
			RETURNF 1
	ENDSELECT

RETURNF 0


;---------------------------------------------
;子宮があなた以外の精子で満たされているかチェック
;@param ARG:0 チェックするキャラクター
;@return RESULT:0 子宮が精液で満たされている率（％）
;@return RESULT:1 あなたの精液が混じっている場合は0
@COM417_GetFillingRateByPublicSperm(ARG:0)
	VARSET LOCAL

	LOCAL:1 = 1
	FOR LOCAL:0, 0, 301
		;ループ中に変数いじるの怖いなあ……
		SIF LOCAL:0 == CHARANUM
			LOCAL:0 = 299

		;精子量を再計算
		DA:(ARG:0):(LOCAL:0) = 0
		REPEAT 5
			DA:(ARG:0):(LOCAL:0) += TA:(ARG:0):(LOCAL:0):COUNT
		REND

		;精子量チェック
		IF DA:(ARG:0):(LOCAL:0) > 0
			IF LOCAL:0 == MASTER
				LOCAL:1 = 0
			ENDIF
			LOCAL:2 += DA:(ARG:0):(LOCAL:0)
			LOCAL:3 ++
		ENDIF
	NEXT
	
	DEBUGPRINTFORM %CALLNAME:(ARG:0)%の子宮内は
	IF LOCAL:1
		DEBUGPRINTFORM %CALLNAME:MASTER%以外の
	ELSE
		DEBUGPRINTFORM %CALLNAME:MASTER%を含めた
	ENDIF
	DEBUGPRINTFORML {LOCAL:3}人の精液で{100*(LOCAL:2)/(CFLAG:(ARG:0):子宮内体積)}％満たされています。

	RETURN LIMIT(100*(LOCAL:2)/(CFLAG:(ARG:0):子宮内体積),0,200),LOCAL:1

RETURN 0,0

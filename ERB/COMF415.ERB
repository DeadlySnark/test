;食事をふるまう
@COM415
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	EXP:MASTER:会話経験 ++
	TFLAG:193 = 1
ELSEIF LOCAL >= LOCAL:1
	;10〜1％で失敗
	TFLAG:193 = -1
ELSE
	;残りは成功
	TFLAG:193 = 0
ENDIF
;先制発生時は失敗しない
SIF TCVAR:20
	TFLAG:193 = MAX(TFLAG:193,0)

DOWNBASE:MASTER:気力 += 100
DOWNBASE:MASTER:体力 += 50

	;その場に居るTARGET全員で回す
	LOCAL:1 = TARGET
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):睡眠
			CONTINUE
		TARGET = TARGET:LOCAL
		CALL COM415_1
	NEXT
	TARGET = LOCAL:1

TIME += 60
TCVAR:PLAYER:305 = 0
TCVAR:PLAYER:306 = 0
RETURN 1

@COM415_1

DOWNBASE:気力 += 10

;固定で獲得するソース
SELECTCASE TCVAR:PLAYER:305
	CASE 0
		SOURCE:歓楽 = 1600
	CASE 1
		SOURCE:歓楽 = 400
	CASE 2
		SOURCE:歓楽 = 100
		SOURCE:不潔 = 600
		SOURCE:反感 = 600
ENDSELECT

;ABL:従順をみる
IF ABL:従順 <= 1
	SOURCE:歓楽 += (ABL:従順 * 60)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 300 + (ABL:従順 * 60)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 600 + (ABL:従順 * 60)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 900 + (ABL:従順 * 75)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 1500 + (ABL:従順 * 75)
ELSE
	SOURCE:歓楽 += 2500 + (ABL:従順 * 30)
ENDIF

;好感度をみる
IF CFLAG:好感度 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:好感度 <= 5000
	SOURCE:歓楽 += 1000 + (CFLAG:好感度 - 500) / 3
ELSE
	SOURCE:歓楽 += 2500 + (CFLAG:好感度 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0


SOURCE:受動 = 500 + 100 * ABL:従順
SOURCE:征服 = 500 + 100 * ABL:サドっ気

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TIMES SOURCE:反感 , 2.00
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TIMES SOURCE:反感 , 0.50
ENDIF

IF FLAG:料理 < 10
	BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 20 , MAXBASE:体力)
	BASE:気力 = MIN(BASE:気力 + MAXBASE:気力 / 5 , MAXBASE:気力)
	TCVAR:304 |= 1
ELSEIF FLAG:料理 >= 10 && FLAG:料理 < 20
	BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 10 , MAXBASE:体力)
	BASE:気力 = MIN(BASE:気力 + MAXBASE:気力 / 3 , MAXBASE:気力)
	TCVAR:304 |= 2
ELSEIF FLAG:料理 >= 50 && FLAG:料理 < 60
	BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 15 , MAXBASE:体力)
	BASE:気力 = MIN(BASE:気力 + MAXBASE:気力 / 2 , MAXBASE:気力)
	TCVAR:304 |= 4
ENDIF

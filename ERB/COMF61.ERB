;-------------------------------------------------
;後背位
;セックス系コマンド、レベル1
;-------------------------------------------------
@COM61

TRYCALL COM417_is_Protected(PLAYER,TARGET,1)
SIF RESULT
	RETURN 0

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回の調教が3Pのとき3Pへ
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT == 1
		JUMP COM71
ENDIF
;ベッドがないと立位に
SIF !BEDROOM(CFLAG:PLAYER:現在位置)
	TFLAG:193 = 1
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 1
;強度
TCVAR:PLAYER:4 = 1800

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 50
DOWNBASE:気力 += 100

SOURCE:快Ｖ = 400
SOURCE:情愛 = 150
SOURCE:苦痛 = 400
SOURCE:露出 = 70
SIF TFLAG:193
	SOURCE:露出 = 300
SOURCE:反感 = 300

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 300

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操 > 0 && !TALENT:恋慕
	SOURCE:反感 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 1
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 2
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
SIF TALENT:処女
	STAIN:膣内 |= 64
;奴隷の膣内⇔調教者のＰの汚れが移動
STAIN:PLAYER:Ｐ |= STAIN:Ｖ
STAIN:PLAYER:Ｐ |= STAIN:膣内
STAIN:Ｖ |= STAIN:PLAYER:Ｐ
STAIN:膣内 |= STAIN:PLAYER:Ｐ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ｖ性交経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF

;愛情経験
IF !ASSIPLAY
	EXP:愛情経験 += TALENT:恋慕 * 2
	;処女
	SIF TALENT:処女 && TALENT:恋慕
		EXP:愛情経験 += 5
ENDIF
RETURN 1

@CAN_COM61(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF !TEQUIP:縄
	SKIPDISP ARG
	IF !BEDROOM(CFLAG:PLAYER:現在位置)
		PRINTL 背面立位
	ELSE
		PRINTL 後背位
	ENDIF
	A = 0
	S = 0
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 3
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF S
			PRINT  + 
		A += ABL:Ｖ感覚 * 2
		PRINT Ｖ感覚
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		S = 1
	ENDIF
	;快楽刻印
	IF MARK:快楽刻印
		SIF S
			PRINT  + 
		A += MARK:快楽刻印 * 3
		PRINT 快楽刻印
		PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
		S = 1
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 3
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:羞恥心 > 0
		PRINT  - 
		A -= 2
		PRINT 恥じらい
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感応答 < 0 && !TALENT:恋慕
		PRINT  - 
		A -= 5
		PRINT 快感の否定
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF TALENT:性別嗜好 & 1 && !(HAS_VAGINA(PLAYER)) && (!TALENT:恋慕 || PLAYER != MASTER)
		PRINT  - 
		A -= 12
		PRINT 男嫌い
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && !ASSIPLAY
		SIF S
			PRINT  + 
		A += 15
		PRINT 恋慕
		PRINTV '(,15,')
		S = 15
	ENDIF
	;親愛
	IF TALENT:恋慕 > 1 && !ASSIPLAY
		SIF S
			PRINT  + 
		A += 5
		PRINT 親愛
		PRINTV '(,5,')
		S = 5
	ENDIF
	;処女
	LOCAL = MAX(10,40 - EXP:Ｖ経験)
	IF TALENT:処女
		PRINT  - 
		A -= LOCAL
		PRINT 処女
		PRINTV '(,LOCAL,')
		S = 1
	;V経験
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		A -= 15
		PRINT Ｖ経験不足
		PRINTV '(,15,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF GET_SEXNAME(PLAYER) == "ふたなり"
		SIF S
			PRINT  + 
		A += 4
		PRINT ふたなり
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:媚薬
		SIF S
			PRINT  + 
		A += 6
		PRINT 媚薬
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:Ｐ & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:Ｐ & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:Ｐ & 8
		Y += 7
	SIF STAIN:PLAYER:Ｐ & 16
		Y += 15
	Y = Y * (2 - TALENT:汚臭耐性) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT 汚れあり
		;汚臭鈍感
		IF TALENT:汚臭耐性 > 0
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭耐性 < 0
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF
	;鍵を持っていると実行可能
	IF FLAG:貞操帯鍵購入フラグ == TARGET
		SIF S
			PRINT  + 
		A += 100
		PRINT 貞操帯の鍵
		PRINTV '(,100,')
		S = 100
	ENDIF	

	;合計を表示(22以上で実行)
	PRINT  = 
	PRINTV A

	V = 22
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;COMABLE一括管理
@GLOBAL_COMABLE(ARG)
	#FUNCTION
	;コマンドフィルタ
	LOCALS = /{ARG}/
	IF STRCOUNT(SAVESTR:0,LOCALS)
		RESULT = 0
		RETURNF 1
	ENDIF
	;一括管理
	;TFLAG:102,(1=日常ON 2=ウフフON)
	IF ARG >= 300 && ARG < 310
		;会話(300)、お茶を入れる(301)、スキンシップ(302)などは日常モード限定
		IF TFLAG:102 != 1
			RESULT = 0
			RETURNF 1
		ENDIF
	ELSEIF ARG >= 310 && ARG < 400
		;尻を撫でる(310)〜指挿入(316)などは日常モード限定
		IF TFLAG:102 == 2
			RESULT = 0
			RETURNF 1
		ENDIF
		;お風呂場ではガチエロになるので、押し倒され状態でない場合、連れ出す(351)と押し倒す(350)以外は不可
		IF BATHROOM(CFLAG:MASTER:現在位置) && CFLAG:うふふ != 2
			IF ARG != 350 && ARG != 351
				RESULT = 0
				RETURNF 1
			ENDIF
		ENDIF
	ELSEIF ARG < 200 || ARG >= 500 && ARG < 600
		;0〜100類(通常調教コマンド)、500類(多人数プレイ)は、うふふOFFの場合不可、自慰の場合も不可
		IF TFLAG:102 != 2 || CFLAG:うふふ == 3
			RESULT = 0
			RETURNF 1
		ENDIF
	ELSEIF ARG >= 200 && ARG < 300
		;200系（脱衣）はうふふONでなければ不可
		IF TFLAG:102 != 2
			RESULT = 0
			RETURNF 1
		ENDIF
	ELSEIF ARG >= 400 && ARG < 500
		;400系(休息、コレクションなどあなた特殊系コマンド)はうふふONの場合不可
		IF TFLAG:102 == 2
			RESULT = 0
			RETURNF 1
		ENDIF
	ELSEIF ARG >= 600
		;600系（自慰専用）は自慰状態じゃない場合不可
		IF CFLAG:うふふ != 3
			RESULT = 0
			RETURNF 1
		ENDIF
	ENDIF
	IF TARGET:1 > 0 && CFLAG:睡眠 == 1 && (ARG < 400 || ARG >= 500)
		;寝ている相手には大半のコマンドは不可
		RESULT = 0
		RETURNF 1
	ENDIF
	;ビデオ処理（固定カメラならば制限なし）
	;撮影者が調教中、ビデオ片手ではいろいろ制限される
	IF (TFLAG:105 == 1 && !ASSIPLAY) || (TFLAG:105 == 2 && ASSIPLAY)
		IF ARG == 0 || ARG == 3 || ARG == 5 || ARG == 6 || ARG == 7 || ARG == 8 || ARG == 9 || ARG == 21 || (ARG >= 40 && ARG <= 48) || ARG == 65 || ARG == 66 || (ARG >= 80 && ARG <= 87) || (ARG >= 100 && ARG <= 105) || (ARG >= 122 && ARG <= 125) || (ARG >= 140 && ARG <= 148 && ARG != 143) || (ARG >= 180 && ARG <= 189) || ARG == 503 || ARG == 504 || ARG == 510 || ARG == 511
			RETURNF 0
		ELSE
			RESULT = 0
			RETURNF 1
		ENDIF
		;MASTERが撮影中、助手が調教中
	ELSEIF TFLAG:105 == 1 && ASSIPLAY
		IF ARG == 71 || ARG == 124 || ARG == 125 || ARG == 185
			RESULT = 0
			RETURNF 1
		ENDIF
		;助手が撮影中、MASTERが調教中
	ELSEIF TFLAG:105 == 2 && !ASSIPLAY
		IF ARG == 71 || ARG == 124 || ARG == 125 || ARG == 185 || ARG == 510 || ARG == 511
			RESULT = 0
			RETURNF 1
		ENDIF
	ENDIF

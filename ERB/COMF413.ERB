;-------------------------------------------------
;料理を作る
;-------------------------------------------------
@COM413
CALL COOKING_MENU
SIF !RESULT
	RETURN 0

IF TFLAG:194 == 1
	DOWNBASE:MASTER:体力 = 25
	DOWNBASE:MASTER:気力 = 100
	TIME += 30

ELSEIF TFLAG:194 == 2
	DOWNBASE:MASTER:体力 = 50
	DOWNBASE:MASTER:気力 = 200
	TIME += 60
ELSEIF TFLAG:194 == 3
	DOWNBASE:MASTER:体力 = 100
	DOWNBASE:MASTER:気力 = 100
	TIME += 90
ENDIF

IF TARGET > 0 && !CFLAG:睡眠
	IF TARGET && ABL:親密 * 500 + ABL:従順 * 500 + ABL:料理技能 * 1000 > CFLAG:館内地位 - CFLAG:MASTER:館内地位
		TFLAG:193 = 1
		EXP:料理経験 ++
	ELSE
		TFLAG:193 = 0
	ENDIF
	;固定で獲得するソース
	SOURCE:歓楽 = 200

	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:歓楽 += (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 3
		SOURCE:歓楽 += 200 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 5
		SOURCE:歓楽 += 500 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 8
		SOURCE:歓楽 += 750 + (ABL:従順 * 75)
	ELSEIF ABL:従順 <= 11
		SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
	ELSE
		SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
	ENDIF

	;好感度をみる
	IF CFLAG:好感度 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:好感度 <= 5000
		SOURCE:歓楽 += 500 + (CFLAG:好感度 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:好感度 - 5000) / 5
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0
ENDIF

;SIF TARGET < 1
;	CALL MIX_MENU

EXP:MASTER:料理経験 ++
TCVAR:PLAYER:305 = 1440 * DAY + TIME
RETURN 1

@COOKING_MENU
PRINTL 何を作りますか？
PRINTL [0] - やっぱりやめた
PRINTL [1] - 軽食
SIF ABL:MASTER:料理技能 > 1 && !(TCVAR:PLAYER:304 & 2)
	PRINTL [2] - 主食
SIF ABL:MASTER:料理技能 > 2 && !(TCVAR:PLAYER:304 & 4)
	PRINTL [3] - デザート 

$INPUT_LOOP
INPUT

IF RESULT == 1
	LOCAL = 1
	WHILE LOCAL
		LOCAL:1 = RAND:10
		SIF STRLENS(GETDISHNAME(LOCAL:1))
			LOCAL = 0
	WEND
	FLAG:料理 = LOCAL:1
	PRINTFORMW %GETDISHNAME(LOCAL:1)%を作ります

	TFLAG:194 = 1
	RETURN 1
ELSEIF RESULT == 2 && ABL:MASTER:料理技能 > 1 && !(TCVAR:PLAYER:304 & 2)
	LOCAL = 1
	WHILE LOCAL
		LOCAL:1 = RAND:10
		LOCAL:1 += 10
		SIF STRLENS(GETDISHNAME(LOCAL:1))
			LOCAL = 0
	WEND
	FLAG:料理 = LOCAL:1
	PRINTFORMW %GETDISHNAME(LOCAL:1)%を作ります

	TFLAG:194 = 2
	RETURN 1
ELSEIF RESULT == 3 && ABL:MASTER:料理技能 > 2 && !(TCVAR:PLAYER:304 & 4)
	LOCAL = 1
	WHILE LOCAL
		LOCAL:1 = RAND:10
		LOCAL:1 += 50
		SIF STRLENS(GETDISHNAME(LOCAL:1))
			LOCAL = 0
	WEND
	FLAG:料理 = LOCAL:1
	PRINTFORMW %GETDISHNAME(LOCAL:1)%を作ります

	TFLAG:194 = 3
	RETURN 1
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF

@MIX_MENU
PRINTL 何か混ぜますか？
PRINTL [0] - いいえ
PRINTL [1] - はい
$INPUT_LOOP
INPUT
IF RESULT == 1
	CALL MIX_SELCT
	RETURN 1
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF

@MIX_SELCT
PRINTL 何を混ぜようか…
SIF HAS_PENIS(MASTER)
	PRINTL [1] - 精子
SIF HAS_VAGINA(MASTER)
	PRINTL [2] - 愛液
SIF TALENT:MASTER:母乳体質
	PRINTL [3] - 母乳
SIF ITEM:41 != 0
	PRINTL [4] - 媚薬
SIF ITEM:42 != 0
	PRINTL [5] - 利尿剤
SIF TALENT:MASTER:調合知識
	PRINTL [6] - 睡眠薬
PRINTL [100] - やっぱりやめた
$INPUT_LOOP
INPUT
	IF RESULT == 1 && HAS_PENIS(MASTER)
		PRINTFORML %CALLNAME:PLAYER%は作った%GETDISHNAME(FLAG:料理)%に向かってペニスを扱きたてた
		PRINTFORML %GETDISHNAME(FLAG:料理)%に%CALLNAME:PLAYER%の放った精液が降りかかった…
		EXP:MASTER:自慰経験 ++
		EXP:MASTER:射精経験 ++
		RETURN 1
	ELSEIF RESULT == 2 && HAS_VAGINA(MASTER)
		PRINTFORML %CALLNAME:PLAYER%は自身の身体を慰めながら作った%GETDISHNAME(FLAG:料理)%を股の下にくるようにした 
		PRINTFORML %GETDISHNAME(FLAG:料理)%に%CALLNAME:PLAYER%の秘所から垂れた愛液がかかった…
		EXP:MASTER:自慰経験 ++
		RETURN 1
	ELSEIF RESULT == 3 && TALENT:MASTER:母乳体質
		PRINTFORML %CALLNAME:PLAYER%は胸を曝け出すと、自身で胸を絞り出した
		PRINTFORML %GETDISHNAME(FLAG:料理)%に%CALLNAME:PLAYER%の母乳がかけられた…
		EXP:MASTER:噴乳経験 ++
		RETURN 1
	ELSEIF RESULT == 4 && ITEM:41 != 0
		PRINTFORML %CALLNAME:PLAYER%は懐から媚薬を出すと、%GETDISHNAME(FLAG:料理)%に混ぜた…
		ITEM:41 -= 1
		RETURN 1
	ELSEIF RESULT == 5 && ITEM:42 != 0
		PRINTFORML %CALLNAME:PLAYER%は懐から利尿剤を出すと、%GETDISHNAME(FLAG:料理)%に混ぜた…
		ITEM:42 -= 1
		RETURN 1
	ELSEIF RESULT == 6 && TALENT:MASTER:調合知識
		PRINTFORML %CALLNAME:PLAYER%は懐から睡眠薬を出すと、%GETDISHNAME(FLAG:料理)%に混ぜた…
		RETURN 1
	ELSEIF RESULT == 100
		RETURN 0
	ELSE
		GOTO INPUT_LOOP
	ENDIF

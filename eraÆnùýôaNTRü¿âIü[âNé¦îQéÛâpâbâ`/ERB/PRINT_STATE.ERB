;-------------------------------------------------------------------------------
;キャラクターの能力表示を行う
;@param ARG:0 表示するキャラクター番号
;@param ARG:1 宝珠を表示 1:する 0:しない
;@param ARG:2 何処から呼び出したか 1:SHOP 0:それ以外
;@return 1:xxx,0:xxx
@PRINT_STATE(ARG, ARG:1, ARG:2)
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTS NAME:ARG
	;成人以外の子供のみ表示される
	SIF (NO:ARG == 148 || NO:ARG == 149)
		PRINTFORM %子供の成長状態(ARG)%
	PRINTFORML (好感度:{CFLAG:ARG:好感度})
	PRINT %NTR_NAME(0)%からの脅迫
	PRINTFORML (屈服度:{CFLAG:ARG:屈服度})
	PRINT 体力:
	BAR BASE:ARG:0,MAXBASE:ARG:0,16
	PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4}) 
	PRINT 気力:
	BAR BASE:ARG:1,MAXBASE:ARG:1,16
	PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
	PRINTL
	PRINTL
IF LOCAL == 0
	PRINTL ＝＝メイン情報＝＝
	CALL PRINT_STATE_CLOTH(ARG)
	CALL PRINT_STATE_ABL(ARG)
	PRINTL
	CALL PRINT_STATE_TALENT(ARG)
	IF ARG != MASTER
		PRINTL
		PRINTL □ 刻印 □---------------------------------------------------------------------
		PRINT_MARK ARG
	ENDIF
	CALL PRINT_STATE_EXP(ARG)
	SIF ARG != MASTER && !ARG:1
		CALL PRINT_STATE_JUEL(ARG)
	SIF ARG:1
		CALL PRINT_STATE_EX(ARG)
	PRINTL
	PRINTL □ その他 □-------------------------------------------------------------------
	TRYCALL PRINT_STATE_PROTECTION(ARG)
	PRINTL
	TRYCALL PRINT_RELATION(ARG)
ELSEIF LOCAL == 1
	PRINTL ＝＝親子情報１＝＝
	PRINTL
	PRINTL □ 兄弟関係 □-----------------------------------------------------------------
	TRYCALL PRINT_PARENT_3(ARG)
	PRINTL
	PRINTL □ 親 □-----------------------------------------------------------------------
	TRYCALL PRINT_PARENT(ARG)
	PRINTL
	PRINTL □ 子供 □---------------------------------------------------------------------
	TRYCALL PRINT_PARENT_2(ARG)
	PRINTL
ELSEIF LOCAL == 2
	PRINTL ＝＝親子情報２＝＝
	PRINTL
	PRINTL □ 出産表 □-------------------------------------------------------------------
	TRYCALL PRINT_PARENT_4(ARG)
	PRINTL
ENDIF

DRAWLINE
IF LOCAL != 0
	PRINTC [1] 前のページへ
ELSE
	PRINTC 　
ENDIF

IF LOCAL != 2
	PRINTC [2] 次のページへ
ELSE
	PRINTC 　
ENDIF
PRINTL
IF ARG:2
	IF ARG != 0
		PRINTC [3] 前のキャラへ
	ELSE
		PRINTC 　
	ENDIF
	IF ARG != CHARANUM-1
		PRINTC [4] 次のキャラへ
	ELSE
		PRINTC 　
	ENDIF
ENDIF
PRINTL
PRINTC [9] 戻る
PRINTL

$LOOP_STATE
ONEINPUT
IF RESULT == 1 && LOCAL != 0
	LOCAL --
	RESTART
ELSEIF RESULT == 2 && LOCAL != 2
	LOCAL ++
	RESTART
ELSEIF RESULT == 3 && ARG != 0
	ARG --
	RESTART
ELSEIF RESULT == 4 && ARG != CHARANUM-1
	ARG ++
	RESTART
ELSEIF RESULT != 9
	GOTO LOOP_STATE
ENDIF


;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@PRINT_STATE_CLOTH(ARG)
	PRINTL
	PRINTL □ 服装 □---------------------------------------------------------------------
	VARSET LOCAL
	IF !EQUIP:ARG:0
		PRINTL 全裸
		RETURN 0
	ENDIF
	FOR LOCAL,1,20
		IF EQUIP:ARG:LOCAL
			PRINTFORML %CLOTHNAME(LOCAL,EQUIP:ARG:LOCAL)%
			LOCAL:1 ++
		ENDIF
	NEXT
	SIF !LOCAL:1
		PRINTL 全裸

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@PRINT_STATE_ABL(ARG)
	PRINTL
	PRINTL □ 能力 □---------------------------------------------------------------------
	LOCAL:1 = 0
	FOR LOCAL,0,50
		SIF !STRLENS(ABLNAME:LOCAL)
			CONTINUE
		SIF LOCAL > 50 && ARG != MASTER
			CONTINUE
		;同性愛関係
		IF !(HAS_VAGINA(ARG)) && (LOCAL == 17 || LOCAL == 32)
			CONTINUE
		ELSEIF HAS_VAGINA(ARG) && (LOCAL == 18 || LOCAL == 33)
			CONTINUE
		ENDIF		
		PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
		LOCAL:1 ++
		SIF LOCAL:1 % 4 == 0
			PRINTL 
	NEXT
	PRINTL
	PRINTL □ 性技 □---------------------------------------------------------------------
	LOCAL:1 = 0
	FOR LOCAL,50,60
		SIF !STRLENS(ABLNAME:LOCAL)
			CONTINUE
		SIF !(HAS_VAGINA(ARG)) && (LOCAL == 52 || LOCAL == 54)
			CONTINUE
		PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
		LOCAL:1 ++
		SIF LOCAL:1 % 4 == 0
			PRINTL 
	NEXT

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@PRINT_STATE_TALENT(ARG)
	PRINTL □ 素質 □---------------------------------------------------------------------
	LOCAL:1 = 0
	LOCALS = ""
	;TALENT:200(名無し)は表示させない
	FOR LOCAL,0,200
		IF STRLENS(GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL))
			;童貞
			SIF LOCAL == 1 && !(HAS_PENIS(ARG))
				CONTINUE
			LOCALS += GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL)
			PRINTFORM [%GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL)%]
			IF STRLENS(LOCALS) > 50
				LOCALS = ""
				PRINTL
			ENDIF
		ENDIF
	NEXT

;-------------------------------------------------------------------------------
;娼婦紋の表示（あれば）
@PRINT_STATE_PROTECTION(ARG)
	VARSET LOCALS
	IF CFLAG:ARG:娼婦紋 == 0
		RETURN 0
	ENDIF
	IF GETBIT(CFLAG:ARG:娼婦紋,0)
		IF LOCALS:0 != ""
			LOCALS:0 = %LOCALS:0 + "、"%
		ENDIF
		LOCALS:0 = %LOCALS:0 + "恥丘"%
	ENDIF
	IF GETBIT(CFLAG:ARG:娼婦紋,1)
		IF LOCALS:0 != ""
			LOCALS:0 = %LOCALS:0 + "、"%
		ENDIF
		LOCALS:0 = %LOCALS:0 + "太腿"%
	ENDIF
	IF GETBIT(CFLAG:ARG:娼婦紋,2)
		IF LOCALS:0 != ""
			LOCALS:0 = %LOCALS:0 + "、"%
		ENDIF
		LOCALS:0 = %LOCALS:0 + "左尻"%
	ENDIF
	IF GETBIT(CFLAG:ARG:娼婦紋,3)
		IF LOCALS:0 != ""
			LOCALS:0 = %LOCALS:0 + "、"%
		ENDIF
		LOCALS:0 = %LOCALS:0 + "胸元"%
	ENDIF
	IF GETBIT(CFLAG:ARG:娼婦紋,4)
		IF LOCALS:0 != ""
			LOCALS:0 = %LOCALS:0 + "、"%
		ENDIF
		LOCALS:0 = %LOCALS:0 + "右頬"%
	ENDIF
	;子宮内が他人の精子で満たされているかどうかチェック
	CALL COM417_GetFillingRateByPublicSperm(ARG)
	SELECTCASE RESULT:1 * RESULT:0
		CASE IS >= 200
			PRINTFORML %"鮮やかなピンク色の娼婦紋("+LOCALS:0+")"%
		CASE 100 TO 199
			PRINTFORML %"ピンク色の娼婦紋("+LOCALS:0+")"%
		CASE 50 TO 99
			PRINTFORML %"赤色の娼婦紋("+LOCALS:0+")"%
		CASE 1 TO 49
			PRINTFORML %"赤紫色の娼婦紋("+LOCALS:0+")"%
		CASEELSE
			PRINTFORML %"紫色の娼婦紋("+LOCALS:0+")"%
	ENDSELECT
RETURN 0

;-------------------------------------------------
;関数名:GET_TALENTNAME
;概　要:素質名取得関数
;引　数:ARG:0…TALENT登録番号
;　　　:ARG:1…TALENTの中身
;戻り値:素質名
;備　考:文中関数
;素質の名前を返す。
;-------------------------------------------------
@GET_TALENTNAME(ARG,ARG:1)
	#FUNCTIONS
	SELECTCASE ARG
			;処女
		CASE 0
			SIF ARG:1
				RETURNF "処女"
			;童貞
		CASE 1
			SIF !ARG:1
				RETURNF "童貞"
			;性別
		CASE 2
			IF ARG:1 == 3
				RETURNF "ふたなり"
			ELSEIF ARG:1 == 2
				RETURNF "オトコ"
			ENDIF
			;淫乱
		CASE 4
			IF ARG:1 == 1
				RETURNF "淫乱"
			ELSEIF ARG:1 == 2
				RETURNF "娼婦"
			ENDIF
			;度胸
		CASE 10
			IF ARG:1 == -1
				RETURNF "臆病"
			ELSEIF ARG:1 == 1
				RETURNF "気丈"
			ENDIF
			;態度
		CASE 11
			IF ARG:1 == -1
				RETURNF "素直"
			ELSEIF ARG:1 == 1
				RETURNF "反抗的"
			ENDIF
			;応答
		CASE 12
			IF ARG:1 == -1
				RETURNF "大人しい"
			ELSEIF ARG:1 == 1
				RETURNF "生意気"
			ENDIF
			;プライド
		CASE 13
			IF ARG:1 == -1
				RETURNF "プライド低い"
			ELSEIF ARG:1 == 1
				RETURNF "プライド高い"
			ENDIF
			;性的興味
		CASE 23
			IF ARG:1 == -1
				RETURNF "保守的"
			ELSEIF ARG:1 == 1
				RETURNF "好奇心"
			ENDIF
			;陽気／陰気,
		CASE 24
			IF ARG:1 == -1
				RETURNF "陰気"
			ELSEIF ARG:1 == 1
				RETURNF "陽気"
			ENDIF
			;貞操観念
		CASE 30
			IF ARG:1 == -1
				RETURNF "貞操無頓着"
			ELSEIF ARG:1 == 1
				RETURNF "貞操観念"
			ENDIF
			;自己愛
		CASE 31
			IF ARG:1 == -1
				RETURNF "抑圧"
			ELSEIF ARG:1 == 1
				RETURNF "解放"
			ENDIF
			;羞恥心
		CASE 33
			IF ARG:1 == -1
				RETURNF "恥薄い"
			ELSEIF ARG:1 == 1
				RETURNF "恥じらい"
			ENDIF
			;痛覚
		CASE 40
			IF ARG:1 == -1
				RETURNF "痛みに強い"
			ELSEIF ARG:1 == 1
				RETURNF "痛みに弱い"
			ENDIF
			;濡れやすさ
		CASE 41
			IF ARG:1 == -1
				RETURNF "濡れにくい"
			ELSEIF ARG:1 == 1
				RETURNF "濡れやすい"
			ENDIF
			;習得速度
		CASE 50
			IF ARG:1 == -1
				RETURNF "習得遅い"
			ELSEIF ARG:1 == 1
				RETURNF "習得早い"
			ENDIF
			;汚臭耐性
		CASE 61
			IF ARG:1 == -2
				RETURNF "潔癖症"
			ELSEIF ARG:1 == -1
				RETURNF "汚臭敏感"
			ELSEIF ARG:1 == 1
				RETURNF "汚臭鈍感"
			ELSEIF ARG:1 == 2
				RETURNF "汚れ無視"
			ENDIF
			;快感応答
		CASE 70
			IF ARG:1 == -1
				RETURNF "快感の否定"
			ELSEIF ARG:1 == 1
				RETURNF "快感に素直"
			ENDIF
			;性別嗜好
		CASE 81
			IF ARG:1 == -1
				RETURNF "両刀"
			ELSEIF ARG:1 == 3
				RETURNF "人間不信"
			ELSEIF ARG:1 == 1
				RETURNF "男嫌い"
			ELSEIF ARG:1 == 2
				RETURNF "女嫌い"
			ENDIF
			;体型
		CASE 100
			IF ARG:1 == -3
				RETURNF "小人"
			ELSEIF ARG:1 == -2
				RETURNF "幼児体型"
			ELSEIF ARG:1 == -1
				RETURNF "小柄体型"
			ELSEIF ARG:1 == 1
				RETURNF "長身"
			ELSEIF ARG:1 == 2
				RETURNF "巨躯"
			ELSEIF ARG:1 == 3
				RETURNF "巨人"
			ENDIF
			;Ｃ感度
		CASE 101
			IF ARG:1 == -1
				RETURNF "Ｃ鈍感"
			ELSEIF ARG:1 == 1
				RETURNF "Ｃ敏感"
			ENDIF
			;Ｖ感度
		CASE 102
			IF ARG:1 == -1
				RETURNF "Ｖ鈍感"
			ELSEIF ARG:1 == 1
				RETURNF "Ｖ敏感"
			ENDIF
			;Ａ感度
		CASE 103
			IF ARG:1 == -1
				RETURNF "Ａ鈍感"
			ELSEIF ARG:1 == 1
				RETURNF "Ａ敏感"
			ENDIF
			;Ｂ感度
		CASE 104
			IF ARG:1 == -1
				RETURNF "Ｂ鈍感"
			ELSEIF ARG:1 == 1
				RETURNF "Ｂ敏感"
			ENDIF
			;バストサイズ
		CASE 105
			IF ARG:1 == -2
				RETURNF "絶壁"
			ELSEIF ARG:1 == -1
				RETURNF "貧乳"
			ELSEIF ARG:1 == 1
				RETURNF "巨乳"
			ELSEIF ARG:1 == 2
				RETURNF "爆乳"
			ENDIF
			;回復速度
		CASE 130
			IF ARG:1 == -1
				RETURNF "回復遅い"
			ELSEIF ARG:1 == 1
				RETURNF "回復早い"
			ENDIF
		CASEELSE
			SIF ARG:1
				RETURNF @"%TALENTNAME:ARG%"
	ENDSELECT

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@PRINT_STATE_EXP(ARG)
	PRINTL
	PRINTL □ 経験 □---------------------------------------------------------------------
	LOCAL:1 = 0
	FOR LOCAL,0,100
		;EXP.CSVに登録されていなければCONTINUE
		;SIF !STRLENS(EXPNAME:LOCAL)
		;	CONTINUE
		;未経験を表示させない
		SIF !EXP:ARG:LOCAL
			CONTINUE
		;同性愛関係
		IF !(HAS_VAGINA(ARG)) && LOCAL == 51
			CONTINUE
		ELSEIF HAS_VAGINA(ARG) && LOCAL == 52
			CONTINUE
		ENDIF		
		PRINTFORM %EXPNAME:LOCAL,12%:{EXP:ARG:LOCAL,5,LEFT} 
		LOCAL:1 += 1
		SIF LOCAL:1 % 4 == 0
			PRINTL 
	NEXT

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@PRINT_STATE_JUEL(ARG)
	PRINTL
	PRINTL □ 宝珠 □---------------------------------------------------------------------
	LOCAL:1 = 0
	FOR LOCAL,0,22
		;PALAM.CSVに名称が登録されていないとCONTINUE
		SIF !STRLENS(PALAMNAME:LOCAL)
			CONTINUE
		;1000,1000000,単位で区切る
		LOCALS = {JUEL:ARG:LOCAL}
		SIF JUEL:ARG:LOCAL > 1000000
			LOCALS = {JUEL:ARG:LOCAL / 1000}k
		SIF JUEL:ARG:LOCAL > 1000000000
			LOCALS = {JUEL:ARG:LOCAL / 1000000}m
		PRINTFORM %PALAMNAME:LOCAL,6%の珠:%LOCALS,7,LEFT%
		LOCAL:1 += 1
		;改行or表示位置あけ
		SIF LOCAL:1 % 4 == 0
			PRINTL 
	NEXT
	;否定の珠
	LOCALS = {JUEL:ARG:100}
	SIF JUEL:ARG:100 > 1000000
		LOCALS = {JUEL:ARG:100 / 1000}k
	PRINTFORM %PALAMNAME:100,6%の珠:%LOCALS,7,LEFT%

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@PRINT_STATE_EX(ARG)
	SIF EX:ARG:0 + EX:ARG:1 + EX:ARG:2 + EX:ARG:3 + EX:ARG:10 + EX:ARG:11 == 0
		RETURN 0
	LOCAL:1 = 0
	PRINTL
	PRINTL □ 絶頂回数 □-----------------------------------------------------------------
	FOR LOCAL,0,20
		SIF !STRLENS(EXNAME:LOCAL)
			CONTINUE
		SIF !EX:LOCAL
			CONTINUE
		PRINTFORM %EXNAME:LOCAL,12%:{EX:ARG:LOCAL,5,LEFT}
		LOCAL:1 ++
		SIF LOCAL:1 % 4 == 0
			PRINTL
	NEXT
	PRINT
